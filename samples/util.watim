extern "wasi_unstable" "fd_read" fn raw_read(file: i32, iovs: .i32, iovs_count: i32, result: .i32) -> i32
extern "wasi_unstable" "fd_write" fn raw_write(file: i32, iovs: .Iov, iovs_count: i32, nwritten: .i32) -> i32
extern "wasi_unstable" "proc_exit" fn raw_exit(code: i32)

struct Iov {
    ptr: .i32
    len: i32
}

fn exit(code: i32) {
    $code raw_exit
}

fn write(file: i32, pt: .i32, len: i32) -> i32 {
    memory space: Iov 8 4;
    memory writ: i32 4 4;
    local written: i32
    $space.ptr $pt store32
    $space.len $len store32
    $file $space 1 $writ raw_write drop
    $writ load32 #written
    $written $len = if {
        $len
    } else {
        $file $pt !i32 $written + !.i32 $len $written - write $written +
    }
}

fn write_byte(file: i32, b: i32) {
    memory buf: i32 1;
    $buf $b store8
    $file $buf 1 write drop
}

fn print(n: i32) {
    memory buf: i32 16;
    memory buf_reversed: i32 16;
    local l: i32
    local i: i32
    0 #l
    $n 0 = if {
        1 #l // length = 1
        $buf 48 store8 // put '0' in buf
    } else {
        loop {
            $n 0 = if { break }
            $buf !i32 $l + !.i32
            $n 10 % // rightmost digit
            48 + // + ascii 'a'
            store8
            $n 10 / #n // shift right in decimal
            $l 1 + #l
        }
    }
    0 #i
    loop {
        $buf_reversed !i32 $i + !.i32
        $buf !i32 $l 1 - $i - + !.i32 load8
        store8
        $i 1 + #i
        $i $l = if { break }
    }
    1 $buf_reversed $l write drop
}

fn read(file: i32, buf_addr: .i32, buf_size: i32) -> i32 {
    memory space: i32 8 4;
    $space $buf_addr !i32 store32 
    $space !i32 4 + !.i32 $buf_size store32 
    $file $space 1 $space raw_read drop
    $space load32
}

fn parse(pt: .i32, len: i32) -> i32 {
    local n: i32
    local d: i32
    local original-ptr: .i32
    local original-len: i32
    $pt #original-ptr
    $len #original-len
    loop {
        $pt load8 #d
        $d 48 >= $d 58 <= and if { // 48 is ascii '0'
            $n $d 48 - + #n
        } else {
            1 "Failed to parse: '" write drop
            1 $original-ptr $original-len write drop
            1 "'" write drop
            //1 "\n" write drop
            1 10 write_byte
            1 exit
        }
        $pt 1 + #pt // advance pointer
        $len 1 - #len // reduce length
        $len 0 = if { $n break }
        $n 10 * #n
    }
}

fn dup(a: i32) -> i32, i32 {
    $a $a
}
