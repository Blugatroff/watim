import "../std/core.watim" as CORE
import "../std/io.watim" as IO
import "../std/arena.watim" as Arena
import "../std/array.watim" as Array
import "../std/bytes.watim" as Bytes
import "../std/byte-slice.watim" as ByteSlice
import "../std/byte-array.watim" as ByteArray

extern "wasi_snapshot_preview1" "fd_prestat_get" fn fd_prestat_get(fd: i32, prestat: .Prestat) -> i32
extern "wasi_snapshot_preview1" "fd_prestat_dir_name" fn fd_prestat_dir_name(fd: i32, path: .i32, len: i32) -> i32
extern "wasi_snapshot_preview1" "path_open" fn path_open(
    fd: i32, 
    dirflags: i32, 
    path: .i32, 
    path_len: i32, 
    oflags: i32, 
    fs_rights_base: i64, 
    fs_rights_inheriting: i64, 
    fd_flags: i32, 
    res_fd: .i32
) -> i32
extern "wasi_snapshot_preview1" "fd_close" fn close(fd: i32) -> i32

fn create-rights(read: bool, write: bool) -> i64 {
    $read !i64 1 rotl // 1 << 1
    $write !i64 5 rotl
    or
}

struct PreopenedFd {
    path: ByteSlice:T
    fd: i32
}
fn PreopenedFd-new(fd: i32, path: ByteSlice:T) -> PreopenedFd {
    local self: PreopenedFd
    $fd #self.fd
    $path #self.path
    $self
}
struct FileSystem {
    preopened-fds: Array:Array<PreopenedFd>
}
fn FileSystem-new(preopened-fds: Array:Array<PreopenedFd>) -> FileSystem {
    local fs: FileSystem
    $preopened-fds #fs.preopened-fds
    $fs
}

fn init(arena: .Arena:Arena) -> FileSystem, i32 {
    local prestat: Prestat
    0 @code
    3 @fd
    $arena Arena:save @restore
    Array:new<PreopenedFd> @fds
    $arena Arena:temp @temp @temp-save
    loop {
        $fd &prestat fd_prestat_get #code
        // 8 = Bad file descriptor
        $code 8 = if { 
            0 #code
            break 
        }
        $code 0 /= if {
            break
        }
        $prestat.path_len @len

        $arena $len Arena:alloc-bytes @buf
        $fd $buf $len fd_prestat_dir_name #code
        $code 0 /= if {
            break
        }
        $buf $len ByteSlice:new @path
        $temp &fds $fd $path PreopenedFd-new Array:push<PreopenedFd>
        $fd 1 + #fd
    }
    $arena $fds.ptr $fds.len Array:init<PreopenedFd> FileSystem-new $code
    $code 0 /= if {
        $arena $restore Arena:restore
    }
    $temp $temp-save Arena:restore
}
// returns 0, 0 if not found
// returns 0, e if error
fn find-current-dir(fs: .FileSystem) -> i32, i32 {
    0 @i
    loop {
        $i $fs.preopened-fds.len = if { 
            // not found
            2 "failed to find preopened current directory\n" IO:write-all IO:check
            1 CORE:exit
            0 0 break
        }
        &fs.preopened-fds $i Array:get<PreopenedFd> @preopened-fd
        $preopened-fd.path ByteSlice:unpack "." Bytes:is-prefix
        if { $preopened-fd.fd 0 break }
        $i 1 + #i
    }
}

struct Prestat {
    kind: i32
    path_len: i32
}

fn path-dir(path-ptr: .i32, path-len: i32) -> .i32, i32 {
    local index: i32
    $path-ptr $path-len "/" drop load8 Bytes:find-rev if {
        #index $path-ptr $index
    } else {
        drop $path-ptr $path-len
    }
}


fn str-replace(arena: .Arena:Arena, ptr: .i32, len: i32, target-ptr: .i32, target-len: i32, replacement-ptr: .i32, replacement-len: i32) -> ByteArray:T {
    ByteArray:new @new
    $ptr $len $target-ptr $target-len Bytes:find-seq if {
        @index
        $arena &new $ptr $index ByteArray:push
        $arena &new $replacement-ptr $replacement-len ByteArray:push

        $arena
        $ptr $index + $target-len + @next-ptr $next-ptr
        $len $index - $target-len -
        $target-ptr
        $target-len
        $replacement-ptr
        $replacement-len
        str-replace @new-next

        $arena &new &new-next ByteArray:unpack ByteArray:push
        $new
    } else {
        drop $ptr $len ByteArray:init
    }
}
fn str-split(arena: .Arena:Arena, str-ptr: .i32, str-len: i32, del: i32) -> Array:Array<ByteSlice:T> {
    Array:new<ByteSlice:T> @splits
    loop {
        $str-len 0 = if { break }
        $str-ptr $str-len $del Bytes:find if {
            @index
            $arena &splits $str-ptr $index ByteSlice:new Array:push<ByteSlice:T>
            $str-ptr $index + 1 + #str-ptr
            $str-len $index - 1 - #str-len
        } else {
            drop
            $arena &splits $str-ptr $str-len ByteSlice:new Array:push<ByteSlice:T>
            break
        }
    }
    $splits
}

fn normalize(arena: .Arena:Arena, path-ptr: .i32, path-len: i32) -> .i32, i32 {
    $arena Arena:temp @temp @temp-save
    ByteArray:new @out

    $path-ptr $path-len "./" Bytes:is-prefix not if {
        $temp &out "./" ByteArray:push
    }
    $temp &out $path-ptr $path-len ByteArray:push

    $temp &out ByteArray:unpack "//" "/" str-replace #out
    $temp &out ByteArray:unpack "/./" "/" str-replace #out
    $temp &out ByteArray:unpack "./." "." str-replace #out

    $temp &out ByteArray:unpack "/" drop load8 str-split @splits
    Array:new<ByteSlice:T> @outsplits
    0 @i
    loop {
        $i $splits.len = if { break }
        block {
            $i 1 + $splits.len /= if {
                &splits $i 1 + Array:get<ByteSlice:T> ~ ByteSlice:unpack ".." Bytes:eq 
                &splits $i Array:get<ByteSlice:T> ~ ByteSlice:unpack "." Bytes:eq not and
                &splits $i Array:get<ByteSlice:T> ~ ByteSlice:unpack ".." Bytes:eq not and
                if {
                    $i 1 + #i break
                }
            }
            $temp &outsplits &splits $i Array:get<ByteSlice:T> ~ Array:push<ByteSlice:T>
        }
        $i 1 + #i
    }
    $temp &outsplits "/" Array:join-with @out
    $path-ptr $path-len &out ByteArray:unpack Bytes:eq if {
        $arena &out ByteArray:unpack Arena:push-bytes $out.len
    } else {
        $arena &out ByteArray:unpack normalize
    }
    $temp $temp-save Arena:restore
}

fn open-file(fs: .FileSystem, path: .i32, path_len: i32, rights: i64, inheriting_rights: i64) -> i32, i32 {
    local fd: CORE:I32
    $fs find-current-dir @code @parent-fd
    $code 0 /= if {
        0 $code
    } else {
        $parent-fd 0 $path $path_len 0 $rights $inheriting_rights 0 &fd.inner path_open #code
        $fd.inner $code
    }
}

fn read-file(arena: .Arena:Arena, fs: .FileSystem, path: .i32, path-len: i32) -> ByteArray:T, i32 { block {
    $fs
    $path $path-len
    0 0 = 0 1 = create-rights
    0 1 = 0 1 = create-rights
    open-file @code @fd
    $code 0 /= if {
        $fd close drop
        ByteArray:new $code break
    }
    $arena $fd read-all @code
    $code 0 /= if {
        $fd close drop
        $code
    } else {
        $fd close
    }
} }

fn read-all(arena: .Arena:Arena, file: i32) -> ByteArray:T, i32 {
    64 @buf-size
    $arena Arena:temp @temp @temp-save
    $temp $buf-size Arena:alloc-bytes @buf
    ByteArray:new @out

    loop {
        $file $buf $buf-size IO:read @code @read
        $code 0 /= if {
            ByteArray:new $code break
        }
        $read 0 = if {
            $out 0 break
        } else {
            $arena &out $buf $read ByteArray:push
        }
    }
    $temp $temp-save Arena:restore
}

