import "./maybe.watim" as Maybe (Maybe)
import "./io.watim" as IO
import "./byte-array.watim" as ByteArray (ByteArray)
import "./byte-slice.watim" as ByteSlice (ByteSlice)
import "./array.watim" as Array (Array)
import "./arena.watim" as Arena (Arena)
import "./fmt.watim" as Fmt (Formatter)

struct Map<K, V> {
    keys: Array<K>
    values: Array<V>
    compare: (.K, .K -> bool)
}
fn new<K, V>(compare: (.K, .K -> bool)) -> Map<K, V> {
    Array:new<K> Array:new<V> $compare make Map<K, V>
}
fn new-with-capacity<K, V>(arena: .Arena, compare: (.K, .K -> bool), cap: i32) -> Map<K, V> {
    $arena $cap Array:new-with-capacity<K>
    $arena $cap Array:new-with-capacity<V>
    $compare make Map<K, V>
}
fn clone<K, V>(arena: .Arena, self: .Map<K, V>) -> Map<K, V> {
    $arena &self.keys Array:clone<K>
    $arena &self.values Array:clone<V>
    $self.compare make Map<K, V>
}
struct Stupid<T> { inner: T filler: i32 }
fn insert<K, V>(arena: .Arena, self: .Map<K, V>, key: K, value: V) -> Maybe<V> {
    $key 0 make Stupid<K> @key
    0 @i
    loop {
        $i $self.keys.len = if { 
            $arena &self.keys $key.inner Array:push<K>
            $arena &self.values $value Array:push<V>
            make Maybe<V>.None
            break
        }
        $self $i get-key<K, V> &key.inner $self.compare -> if {
            &self.values $i $value Array:set<V>
            make Maybe<V>.Some
            break
        }
        $i 1 + #i
    }
}
fn insert-assert-no-alloc<K, V>(self: .Map<K, V>, key: K, value: V) -> Maybe<V> {
    $key 0 make Stupid<K> @key
    0 @i
    loop {
        $i $self.keys.len = if { 
            &self.keys $key.inner Array:push-assert-no-alloc<K>
            &self.values $value Array:push-assert-no-alloc<V>
            make Maybe<V>.None
            break
        }
        $self $i get-key<K, V> &key.inner $self.compare -> if {
            &self.values $i $value Array:set<V>
            make Maybe<V>.Some
            break
        }
        $i 1 + #i
    }
}
fn remove<K, V>(self: .Map<K, V>, key: .K) -> Maybe<V> {
    $self $key get-index<K, V> match {
        case None -> { make Maybe<V>.None }
        case Some -> {
            @index
            &self.keys $index Array:remove<K> drop
            &self.values $index Array:remove<V> make Maybe<V>.Some
        }
    }
}
fn get<K, V>(self: .Map<K, V>, key: .K) -> Maybe<.V> {
    $self $key get-index<K, V> match {
        case None -> { make Maybe<.V>.None }
        case Some -> { $self flip get-value<K, V> make Maybe<.V>.Some }
    }
}
fn get-index<K, V>(self: .Map<K, V>, key: .K) -> Maybe<i32> {
    0 @i
    loop {
        $i $self size<K, V> = if { make Maybe<i32>.None break }
        $self $i get-key<K, V> $key $self.compare -> if {
            $i make Maybe<i32>.Some break
        }
        $i 1 + #i
    }
}
fn size<K, V>(self: .Map<K, V>) -> i32 {
    $self.keys.len
}
fn get-key<K, V>(self: .Map<K, V>, index: i32) -> .K {
    &self.keys $index Array:get<K>
}
fn get-value<K, V>(self: .Map<K, V>, index: i32) -> .V {
    &self.values $index Array:get<V>
}
fn format-multi-line<K, V>(fmt: .Formatter, format-key: (.Formatter, .K ->), format-value: (.Formatter, .V ->), self: .Map<K, V>) { block {
    $self.keys.len 0 = if {
        $fmt "(Map)" Fmt:write
        break
    }
    $fmt "(Map\n" Fmt:write
    $fmt Fmt:indent
    0 @i
    loop {
        $i $self.keys.len = if { break }
        $fmt Fmt:write-indent
        $fmt &self.keys $i Array:get<K> $format-key ->
        $fmt "=" Fmt:write
        $fmt &self.values $i Array:get<V> $format-value ->
        $i 1 + #i
        $i $self.keys.len /= if {
            $fmt ",\n" Fmt:write
        }
    }
    $fmt Fmt:dedent
    $fmt ")" Fmt:write
} }

fn get-ba<V>(self: .Map<ByteArray, V>, key-ptr: .i32, key-len: i32) -> Maybe<.V> {
    $key-ptr $key-len ByteArray:init @key
    $self &key get<ByteArray, V>
}
fn get-bs<V>(self: .Map<ByteSlice, V>, key-ptr: .i32, key-len: i32) -> Maybe<.V> {
    $key-ptr $key-len make ByteSlice @key
    $self &key get<ByteSlice, V>
    // TODO: this fails to be rejected: $self &key get<ByteArray, V>
}
fn get-bs-with-index<V>(self: .Map<ByteSlice, V>, key-ptr: .i32, key-len: i32) -> Maybe<[.V, i32]> {
    $key-ptr $key-len make ByteSlice @key
    $self &key get-index<ByteSlice, V> match {
        case None -> { make Maybe<[.V, i32]>.None }
        case Some -> {
            @index
            &self.values $index Array:get<V> $index
            [,2] make Maybe<[.V, i32]>.Some
        }
    }
}
fn remove-bs<V>(self: .Map<ByteSlice, V>, key-ptr: .i32, key-len: i32) -> Maybe<V> {
    $key-ptr $key-len make ByteSlice @key
    $self &key remove<ByteSlice, V>
}

