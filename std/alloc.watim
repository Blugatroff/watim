import "./io.watim" as IO
import "./core.watim" as CORE

memory LOGGING: bool 4
fn enable-logging() {
    $LOGGING 0 0 = store
}
fn disable-logging() {
    $LOGGING 0 1 = store
}
fn logging() -> bool {
    $LOGGING !.i32 ~ 0 /=
}
memory ROOT: .i32 4
memory BUF_SIZE: i32 4
memory N_ALLOCS: i32 4
memory N_FREES: i32 4
fn root() -> .i32 {
    $ROOT ~
}
fn set-root(root: .i32) {
    $ROOT $root store
}
fn buf-size() -> i32 {
    $BUF_SIZE ~
}
fn set-buf-size(size: i32) {
    $BUF_SIZE $size store
}
fn n-allocs() -> i32 {
    $N_ALLOCS ~
}
fn n-frees() -> i32 {
    $N_FREES ~
}
fn set-n-allocs(n: i32) {
    $N_ALLOCS $n store
}
fn set-n-frees(n: i32) {
    $N_FREES $n store
}
fn inc-n-allocs() {
    n-allocs 1 + set-n-allocs 
}
fn inc-n-frees() {
    n-frees 1 + set-n-frees
}

fn init(mem: .i32, size: i32) {
    local next: .i32
    
    logging if { 2 "ALLOC RESET: \n" IO:write-all IO:check }
    $mem set-root
    $size set-buf-size
    // Set the next pointer of the init blok to 0 since there is only this blok. 
    0 !.i32 #next
    // Store the next pointer in the blok.
    root $next !i32 store
    // Store the size of the blok.
    root 4 + $size store
    // Mark this blok as unused.
    root 8 + 0 store
    0 set-n-allocs
    0 set-n-frees
}

fn alloc(size: i32) -> .i32 {
    local prev: .i32
    local blok: .i32
    local next: .i32
    local blok-size: i32
    local new-blok: .i32
    local n: i32
    local unused: bool
    $size 4 CORE:align-to #size
    root #prev
    root #blok
    loop {
        $blok ~ !.i32 #next
        $blok 4 + ~ #blok-size
        $blok 8 + ~ 0 = #unused

        // Test whether this blok has enough space for the requested size + the BLOCK_SIZE.
        $blok-size $size 12 + ge $unused and if {
            // Is there enough remaining space to split this blok in two$
            $blok-size $size 12 + 12 + ge if {
                // Calculate the position of the new blok.
                $blok 12 $size + + #new-blok
                // Load the blok after the one we are splitting.
                $blok ~ #n
                // Set the next-pointer of the new blok.
                $new-blok $n store
                // Set the size of the new blok.
                $new-blok 4 + $blok-size 12 - $size - store
                // Mark the new blok as unused.
                $new-blok 8 + 0 store
            } else {
                0 !.i32 #new-blok
            }
            $new-blok !i32 0 = if {
                $blok !..i32 $next store
            } else {
                $new-blok !i32 =>blok

                // only adjust the size of the blok if it was split
                $blok 4 + $size 12 + store
            }
            $blok 8 + 1 store

            root $prev = if {
                root 8 + ~ 0 /= if { // if the root is used
                    $new-blok !i32 0 = if {
                        root find-next-free set-root
                    } else {
                        $new-blok set-root
                    }
                }
            } else {
                $new-blok !i32 0 = if {
                    $prev $blok find-next-free !i32 store
                } else {
                    $prev $new-blok !i32 store
                }
            }
            logging if { 
                2 "ALLOCED ptr: " IO:write-all IO:check 
                2 $blok 12 + !i32 IO:print IO:check
                2 " size: " IO:write-all IO:check 
                2 $size IO:print IO:check
                2 " n-allocs: " IO:write-all IO:check 
                2 n-allocs 1 + IO:print IO:check
                2 IO:newline
            }
            inc-n-allocs
            $blok 12 + break
        }
        $next !i32 0 = if {
            2 "ALLOC FAILED NO MEMORY size: " IO:write-all IO:check 
            2 $size IO:print IO:check 1 "\n" IO:write-all IO:check
            0 0 / CORE:exit
            0 !.i32 break
        }
        $blok #prev
        $next #blok
    }
}

fn find-next-free(blok: .i32) -> .i32 {
    local b: .i32
    root #b
    loop {
        $b !i32 0 = if { 0 !.i32 break }
        $b 8 + ~ 0 =
        $b !i32 $blok !i32 gt
        and
        if {
            $b break
        }
        $b !..i32 ~ #b
    }
}

fn find-prev-free(blok: .i32) -> .i32 {
    local b: .i32
    root #b
    loop {
        $b ~ $blok !i32 ge if {
            $b break
        }
        $b ~ !.i32 #b
    }
}

fn free(ptr: .i32) {
    local blok: .i32
    local next: .i32
    local next-free: .i32
    local b: .i32
    local size: i32
    $ptr 12 - #blok
    $blok ~ !.i32 #next
    $blok find-next-free #next-free
    $blok 4 + ~ 12 - #size

    logging if { 
        2 "FREEING ptr: " IO:write-all IO:check 
        2 $ptr !i32 IO:print IO:check
        2 " size: " IO:write-all IO:check 
        2 $size IO:print IO:check
        2 " n-allocs: " IO:write-all IO:check 
        2 n-allocs n-frees - 1 - IO:print IO:check
        2 IO:newline 
    }
    $blok $next-free !i32 store
    $blok 8 + 0 store

    root !i32 $blok !i32 gt if {
        $blok set-root
    } else {
        $blok root = // The root blok is being freed
        root ~ 0 = // The root has no next blok
        and if {
            // The last blok is being freed, therefore 
            // everything is free and we can simply reset.
            root buf-size init
        } else {
            $blok find-prev-free !..i32 $blok store
       }
    }
    inc-n-frees
    $ptr $size CORE:clear
    merge
}

fn merge() {
    local blok: .i32
    local next: .i32
    local size: i32
    local used: bool
    local next-next: .i32
    local next-size: i32
    local next-used: bool

    root #blok
    loop {
        $blok !i32 0 = if { break }
        $blok ~ !.i32 #next
        $next !i32 0 = if { break }
        
        $blok 4 + ~ #size
        $blok 8 + ~ 0 /= #used
        $next ~ !.i32 #next-next
        $next 4 + ~ #next-size
        $next 8 + ~ 0 /= #next-used

        $blok $size + $next = $used not and $next-used not and if {
            $blok $next-next !i32 store
            $blok 4 + $size $next-size + store
        } else {
            $next #blok
        }
    }
}

fn print-alloc-list(start: .i32) {
    local blok: .i32
    local next: .i32
    local size: i32
    local used: bool
    $start #blok
    2 "BLOCK LIST STARTING AT: " IO:write-all IO:check 2 $blok !i32 IO:print IO:check 2 10 IO:write-byte IO:check
    loop {
        $blok !i32 0 = if { break }
        $blok ~ !.i32 #next
        $blok 4 + ~ #size
        $blok 8 + ~ 0 gt #used

        2 "\t" IO:write-all IO:check 
        2 $blok !i32 IO:print IO:check
        2 ": " IO:write-all IO:check
        2 " " IO:write-all IO:check
        2 $next !i32 IO:print IO:check
        2 " " IO:write-all IO:check
        2 $size IO:print IO:check
        2 " " IO:write-all IO:check
        2 $used !i32 IO:print IO:check
        2 IO:newline 
        
        $next #blok
    }
}

fn size-of-largest-free-blok() -> i32 {
    local blok: .i32
    local max: i32
    root #blok
    loop {
        $blok !i32 0 = if { break }
        $blok 4 + ~ $max CORE:max #max 
        $blok ~ !.i32 #blok
    }
    $max
}

fn is-empty() -> bool {
    usage 0 =
}

fn usage() -> i32 {
    buf-size remaining - 
}

fn remaining() -> i32 {
    local blok: .i32
    local free: i32
    root #blok
    loop {
        $blok !i32 0 = if { break }
        $blok 4 + ~ $free + #free
        $blok ~ !.i32 #blok
    }
    $free
}

fn report(fd: i32) {
    $fd "Memory usage: \n\t" IO:write-all IO:check 
    $fd usage IO:print IO:check
    $fd " bytes,\n\tlargest free blok: " IO:write-all IO:check
    $fd size-of-largest-free-blok IO:print IO:check
    $fd "\n\tactive allocations: " IO:write-all IO:check
    $fd n-allocs n-frees - IO:print IO:check
    $fd "\n\ttotal allocations: " IO:write-all IO:check
    $fd n-allocs IO:print IO:check
    $fd "\n" IO:write-all IO:check
}

