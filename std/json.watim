import "./bytes.watim" as Bytes
import "./result.watim" as Result (Result)
import "./arena.watim" as Arena (Arena)
import "./array.watim" as Array (Array)
import "./map.watim" as Map (Map)

variant Value {
    case Number -> i32 // watim doesn't support floats :<
    case String -> Array<i8>
    case Array -> Array<Value>
    case Object -> Map<Array<i8>, Value>
    case Null
}
fn Value-eq(a: .Value, b: .Value) -> bool {
    $a match {
        case Number -> { ~ $b match {
            case Number -> { ~ = }
            case _ -> { drop drop 0 1 = }
        } }
        case String -> { $b match {
            case String -> { Array:byte-eq }
            case _ -> { drop drop 0 1 = }
        } }
        case Array -> { \Value-eq flip $b match {
            case Array -> { Array:eq<_> }
            case _ -> { drop drop drop 0 1 = }
        } }
        case Object -> { \Value-eq flip $b match {
            case Object -> { Map:eq<_, _> }
            case _ -> { drop drop drop 0 1 = }
        } }
        case Null -> { $b match {
            case Null -> { 1 1 = }
            case _ -> { drop 0 1 = }
        } }
    }
}

struct ParseError {}

fn advance(ptr: ..i8, len: .i32, n: i32) {
    $ptr ~ $n + =>ptr
    $len ~ $n - =>len
}

fn parse(arena: .Arena, ptr: .i8, len: i32) -> Result<Value, ParseError> { block {
    $arena &ptr &len parse-value match {
        case Success -> {}
        case _ -> { break }
    }
    make Result<Value, ParseError>.Success
} }

fn whitespace(ptr: ..i8, len: .i32) {
    loop {
        $len ~ 0 = if { break }
        $ptr ~~ " " drop ~ /=
        $ptr ~~ "\n" drop ~ /= and
        $ptr ~~ "\t" drop ~ /= and
        if { break }
        $ptr ~ 1 + =>ptr
        $len ~ 1 - =>len
    }
}

fn parse-value(arena: .Arena, ptr: ..i8, len: .i32) -> Result<Value, ParseError> { block {
    $ptr $len whitespace
    $len ~ 0 = if {
        make ParseError make Result<Value, ParseError>.Error break
    }
    $ptr ~ $len ~ "null" Bytes:is-prefix if {
        $ptr $len 4 advance
        $ptr $len whitespace
        make Value.Null make Result<Value, ParseError>.Success break
    }
    $ptr ~~ "-" drop ~ = if {
        $ptr $len 1 advance
        $arena $ptr $len parse-number match {
            case Error -> { make Result<Value, ParseError>.Error break }
            case Success -> { 0 flip - }
        }
        $ptr $len whitespace
        make Value.Number make Result<Value, ParseError>.Success break
    }
    $ptr ~~ Bytes:is-ascii-digit if {
        $arena $ptr $len parse-number match {
            case Error -> { make Result<Value, ParseError>.Error break }
            case Success -> { make Value.Number }
        }
        $ptr $len whitespace
        make Result<Value, ParseError>.Success break
    }
    $ptr ~~ "\"" drop ~ = if {
        $arena $ptr $len parse-string match {
            case Error -> { make Result<Value, ParseError>.Error break }
            case Success -> { make Value.String }
        }
        $ptr $len whitespace
        make Result<Value, ParseError>.Success break
    }
    $ptr ~~ "[" drop ~ = if {
        $arena $ptr $len parse-array match {
            case Error -> { make Result<Value, ParseError>.Error break }
            case Success -> { make Value.Array }
        }
        $ptr $len whitespace
        make Result<Value, ParseError>.Success break
    }
    $ptr ~~ "{" drop ~ = if {
        $arena $ptr $len parse-object match {
            case Error -> { make Result<Value, ParseError>.Error break }
            case Success -> { make Value.Object }
        }
        $ptr $len whitespace
        make Result<Value, ParseError>.Success break
    }
    make ParseError make Result<Value, ParseError>.Error
} }

fn parse-number(arena: .Arena, ptr: ..i8, len: .i32) -> Result<i32, ParseError> { block {
    $len ~ 0 = if { make ParseError make Result<i32, ParseError>.Error break }

    0 @n
    loop {
        $ptr ~~ !i32 @d
        $d 48 ge $d 58 le and if {
            $n $d 48 - + #n
        } else {
            make ParseError make Result<i32, ParseError>.Error break
        }
        $ptr $len 1 advance
        $len ~ 0 = if { $n make Result<i32, ParseError>.Success break }
        $ptr ~~ !i32 @d
        $d 48 lt $d 58 gt or if { $n make Result<i32, ParseError>.Success break }
        $n 10 * #n
    }
} }

fn parse-string(arena: .Arena, ptr: ..i8, len: .i32) -> Result<Array<i8>, ParseError> { block {
    $len ~ 0 = if { make ParseError make Result<Array<i8>, ParseError>.Error break }
    $ptr ~~ "\"" drop ~ /= if { make ParseError make Result<Array<i8>, ParseError>.Error break }

    Array:new<i8> @out

    $ptr $len 1 advance
    loop {
        $len ~ 0 = if { make ParseError make Result<Array<i8>, ParseError>.Error break }
        $ptr ~~ "\"" drop ~ = if {
            $ptr $len 1 advance
            $out make Result<Array<i8>, ParseError>.Success break
        }
        $ptr ~~ "\\" drop ~ = if {
            $ptr $len 1 advance
            $len ~ 0 = if { make ParseError make Result<Array<i8>, ParseError>.Error break }
            $ptr ~~ @escape
            block {
                $escape "\"" drop ~ = if { $arena &out "\"" Array:append-raw<_> 1 1 = break }
                $escape "\\" drop ~ = if { $arena &out "\\" Array:append-raw<_> 1 1 = break }
                $escape "b" drop ~ = if { $arena &out 8 !i8 Array:push<_> 1 1 = break }
                $escape "f" drop ~ = if { $arena &out 12 !i8 Array:push<_> 1 1 = break }
                $escape "n" drop ~ = if { $arena &out "\n" Array:append-raw<_> 1 1 = break }
                $escape "r" drop ~ = if { $arena &out "\r" Array:append-raw<_> 1 1 = break }
                $escape "t" drop ~ = if { $arena &out "\t" Array:append-raw<_> 1 1 = break }
                $escape "u" drop ~ = if { 0 0 / drop }
                0 1 =
            }
            $ptr $len 1 advance
            not if {
                make ParseError make Result<Array<i8>, ParseError>.Error break
            }
        } else {
            $arena &out $ptr ~~ Array:push<_>
            $ptr $len 1 advance
        }
    }
} }

fn parse-array(arena: .Arena, ptr: ..i8, len: .i32) -> Result<Array<Value>, ParseError> { block {
    $len ~ 0 = if { make ParseError make Result<Array<Value>, ParseError>.Error break }
    $ptr ~~ "[" drop ~ /= if { make ParseError make Result<Array<Value>, ParseError>.Error break }
    $ptr $len 1 advance
    $ptr $len whitespace
    $len ~ 0 = if { make ParseError make Result<Array<Value>, ParseError>.Error break }
    Array:new<Value> @items
    $ptr ~~ "]" drop ~ = if {
        $ptr $len 1 advance
        $items make Result<Array<Value>, ParseError>.Success break
    }
    loop {
        $arena $ptr $len parse-value match {
            case Error -> { make Result<Array<Value>, ParseError>.Error break }
            case Success -> {}
        }
        $arena flip &items flip Array:push<_>
        $len ~ 0 = if { make ParseError make Result<Array<Value>, ParseError>.Error break }
        $ptr ~~ "]" drop ~ = if {
            $ptr $len 1 advance
            $items make Result<Array<Value>, ParseError>.Success break
        }
        $ptr ~~ "," drop ~ /= if {
            make ParseError make Result<Array<Value>, ParseError>.Error break
        }
        $ptr $len 1 advance
    }
} }

fn parse-object(arena: .Arena, ptr: ..i8, len: .i32) -> Result<Map<Array<i8>, Value>, ParseError> { block {
    $len ~ 0 = if { make ParseError make Result<Map<Array<i8>, Value>, ParseError>.Error break }
    $ptr ~~ "{" drop ~ /= if { make ParseError make Result<Map<Array<i8>, Value>, ParseError>.Error break }
    $ptr $len 1 advance
    $ptr $len whitespace
    $len ~ 0 = if { make ParseError make Result<Map<Array<i8>, Value>, ParseError>.Error break }
    \Array:byte-eq Map:new<_, Value> @entries
    $ptr ~~ "}" drop ~ = if {
        $ptr $len 1 advance
        $entries make Result<Map<Array<i8>, Value>, ParseError>.Success break
    }
    loop {
        $ptr $len whitespace
        $arena $ptr $len parse-string match {
            case Error -> { make Result<Map<Array<i8>, Value>, ParseError>.Error break }
            case Success -> {}
        } @key
        $ptr $len whitespace
        $len ~ 0 = if { make ParseError make Result<Map<Array<i8>, Value>, ParseError>.Error break }
        $ptr ~~ ":" drop ~ /= if { make ParseError make Result<Map<Array<i8>, Value>, ParseError>.Error break }
        $ptr $len 1 advance
        $arena $ptr $len parse-value match {
            case Error -> { make Result<Map<Array<i8>, Value>, ParseError>.Error break }
            case Success -> {}
        } @value
        $arena &entries $key $value Map:insert<_, _> drop
        $len ~ 0 = if { make ParseError make Result<Map<Array<i8>, Value>, ParseError>.Error break }
        $ptr ~~ "}" drop ~ = if {
            $ptr $len 1 advance
            $entries make Result<Map<Array<i8>, Value>, ParseError>.Success break
        }
        $ptr ~~ "," drop ~ /= if {
            make ParseError make Result<Map<Array<i8>, Value>, ParseError>.Error break
        }
        $ptr $len 1 advance
    }
} }

