import "./core.watim" as CORE
import "./io.watim" as IO
import "./byte-slice.watim" as ByteSlice (ByteSlice)
import "./byte-array.watim" as ByteArray (ByteArray)
import "./arena.watim" as Arena (Arena)

struct Formatter {
    indent-level: i32
    write: (i32, .i32, i32 ->)
    user-data: i32
}

fn new(write: (i32, .i32, i32 ->), user-data: i32) -> Formatter {
    make Formatter {
        $write @write
        $user-data @user-data
        0 @indent-level
    }
}
fn write(self: .Formatter, ptr: .i32, len: i32) {
    $self.user-data $ptr $len $self.write ->
}
fn write-indent(self: .Formatter) {
    0 @i
    loop {
        $i $self.indent-level = if { break }
        $self "  " write
        $i 1 + #i
    }
}
fn indent(self: .Formatter) {
    $self.indent-level 1 + #self.indent-level
}
fn dedent(self: .Formatter) {
    $self.indent-level 0 = if {
        "Formatter: tried to dedent below 0" IO:abort
    }
    $self.indent-level 1 - #self.indent-level
}

fn write-to-fd(fd: i32, ptr: .i32, len: i32) {
    $fd $ptr $len IO:write-all IO:check
}

fn to-fd(fd: i32) -> Formatter {
    \write-to-fd $fd new
}

fn stdout() -> Formatter {
    1 to-fd
}

fn print-it<T>(fd: i32, fmt: (.Formatter, .T ->), item: .T) {
    $fd to-fd @formatter
    &formatter $item $fmt ->
}

struct FormatterIntoStringUserData {
    string: .ByteArray
    arena: .Arena
}

fn append-string(user-data: i32, ptr: .i32, len: i32) {
    $user-data !.FormatterIntoStringUserData @user-data
    $user-data.arena $user-data.string $ptr $len ByteArray:push
}

fn into-string(arena: .Arena, target: .ByteArray) -> Formatter {
    $arena Arena:alloc<FormatterIntoStringUserData> @user-data
    $arena #user-data.arena
    $target #user-data.string
    \append-string $user-data !i32 new
}

fn ByteSlice-format(fmt: .Formatter, self: .ByteSlice) {
    $fmt "\"" write
    uninit<CORE:I32> @escaped-char
    0 @i
    loop {
        $i $self.len = if { break }
        $fmt &escaped-char.inner $self.ptr $i + load8 &escaped-char.inner CORE:escape write
        $i 1 + #i
    }
    $fmt "\"" write
}

