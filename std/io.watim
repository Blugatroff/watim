import "./core.watim" as CORE
extern "wasi_unstable" "fd_read" fn raw_read(file: i32, iovs: .Iov, iovs_count: i32, read: .i32) -> i32
extern "wasi_unstable" "fd_write" fn raw_write(file: i32, iovs: .Iov, iovs_count: i32, written: .i32) -> i32

struct Iov {
    ptr: .i32
    len: i32
}

struct I32 { inner: i32 }

fn write(fd: i32, pt: .i32, len: i32) -> i32, i32 {
    memory space: Iov 8
    local writ: I32
    local code: i32
    0 #writ.inner
    $pt #space.ptr
    $len #space.len
    $fd $space 1 &writ.inner raw_write #code
    $writ.inner $code
}

fn write-all-inner(fd: i32, pt: .i32, len: i32) -> i32, i32 {
    local written: i32
    local code: i32
    $fd $pt $len write #code #written
    $code 0 = if {
        $written $len = if {
            $written $code
        } else {
            $fd 
            $pt $written + 
            $len $written - 
            write-all-inner
            flip $written + flip
        }
    } else {
        0 $code
    }
}

fn write-all(fd: i32, pt: .i32, len: i32) -> i32 {
    $fd $pt $len write-all-inner flip drop
}

fn write-byte(file: i32, b: i32) -> i32 {
    memory buf: i32 1
    $buf $b store8
    $file $buf 1 write-all
}

fn read(file: i32, buf_addr: .i32, buf_size: i32) -> i32, i32 {
    memory iov: Iov 8
    local writ: I32
    local code: i32
    $buf_addr #iov.ptr 
    $buf_size #iov.len 
    $file $iov 1 &writ.inner raw_read #code
    $writ.inner $code
}

fn print-to-fd-fallible(fd: i32, n: i32) -> i32 {
    memory buf-reversed: i32 24
    $fd $buf-reversed $n $buf-reversed print-i32-into write-all
}
fn print-i32-into(n: i32, buf-reversed: .i32) -> i32 {
    memory buf: i32 24
    local l: i32
    local i: i32
    0 #l
    $n 0 = if {
        1 #l // length = 1
        $buf 48 store8 // put '0' in buf
    } else {
        loop {
            $n 0 = if { break }
            $buf $l + !.i32
            $n 10 % // rightmost digit
            48 + // + ascii 'a'
            store8
            $n 10 / #n // shift right in decimal
            $l 1 + #l
        }
    }
    0 #i
    loop {
        $buf-reversed !i32 $i + !.i32
        $buf !i32 $l 1 - $i - + !.i32 load8
        store8
        $i 1 + #i
        $i $l = if { break }
    }
    $l
}

fn print(n: i32) {
    1 $n print-to-fd-fallible check
}
fn print-to-fd(fd: i32, n: i32) {
    $fd $n print-to-fd-fallible check
}

fn check(code: i32) {
    $code 0 /= if {
        2 2 "Error Code: " write-all drop $code print-to-fd-fallible drop
        2 "\n" write-all drop
        1 CORE:exit
    }
}

fn abort(msg-ptr: .i32, msg-len: i32) {
    2 $msg-ptr $msg-len write-all check
    1 CORE:exit
}

