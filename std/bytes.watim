import "./core.watim" as CORE

fn eq(ptr-a: .i8, len-a: i32, ptr-b: .i8, len-b: i32) -> bool {
    0 @i
    $len-a $len-b = if {
        loop {
            $i $len-a = if { 0 0 = break }
            $ptr-a $i + ~
            $ptr-b $i + ~
            /= if {
                0 1 = break
            }
            $i 1 + #i
        }
    } else {
        0 1 =
    }
}
fn find(ptr: .i8, len: i32, byte: i8) -> i32, bool {
    0 @i
    loop {
        $i $len = if {
            0 0 1 = break
        }
        $ptr $i + ~ $byte = if {
            $i 0 0 = break
        }
        $i 1 + #i
    }
}
fn find-rev(ptr: .i8, len: i32, byte: i8) -> i32, bool {
    0 @i
    $len #i
    loop {
        $i 0 = if {
            0 0 1 = break
        }
        $ptr $i 1 - + ~ $byte = if {
            $i 0 0 = break
        }
        $i 1 - #i
    }
}
fn is-prefix(ptr: .i8, len: i32, prefix-ptr: .i8, prefix-len: i32) -> bool {
    $ptr $len $prefix-len CORE:min $prefix-ptr $prefix-len eq
}
fn find-seq(ptr: .i8, len: i32, target-ptr: .i8, target-len: i32) -> i32, bool {
    0 @prev-index
    0 @i
    0 @index
    loop {
        $target-len 0 = if { 0 0 0 = break }
        $len 0 = if {
            0 0 1 = break
        }
        $target-ptr ~ @byte
        $ptr $len $byte find if {
            #index
            $ptr $index + $len $index -
            $target-ptr $target-len
            is-prefix if {
                $index $prev-index + 0 0 = break
            }
        } else {
            0 1 = break
        }
        $ptr $index + 1 + #ptr
        $len $index - 1 - #len
        $index 1 + $prev-index + #prev-index
    }
}
fn is-ascii-digit(char: i8) -> bool {
    $char 48 !i8 ge $char 57 !i8 le and
}

