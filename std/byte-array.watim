import "./core.watim" as CORE
import "./io.watim" as IO
import "./arena.watim" as Arena (Arena)
import "./bytes.watim" as Bytes
import "./byte-slice.watim" as ByteSlice (ByteSlice)

struct ByteArray {
    ptr: .i8
    len: i32
    cap: i32
    arena: .Arena
}

fn new() -> ByteArray {
    make ByteArray {
        0 !.i8 @ptr
        0 @len
        0 @cap
        0 !.Arena @arena
    }
}

fn with-capacity(arena: .Arena, cap: i32) -> ByteArray {
    make ByteArray {
        $arena $cap Arena:alloc-bytes @ptr
        $cap @cap
        $arena @arena
        0 @len
    }
}

fn init(ptr: .i8, len: i32) -> ByteArray {
    make ByteArray {
        $ptr @ptr
        $len @len
        $len @cap
        0 !.Arena @arena
    }
}

fn copy(arena: .Arena, ptr: .i8, len: i32) -> ByteArray {
    new @self
    $arena &self $ptr $len push
    $self
}

fn check-same-arena(self: .ByteArray, arena: .Arena) {
    $self.arena 0 !.Arena = if {
        $arena #self.arena
    } else {
        $self.arena $arena /= if {
            2 "ByteArray: used different arenas\n" IO:write-all IO:check
            0 0 / drop
        }
    }
}

fn push(arena: .Arena, self: .ByteArray, ptr: .i8, len: i32) {
    $ptr $arena $self $len alloc-bytes $len CORE:copy-bytes
}
fn alloc-bytes(arena: .Arena, self: .ByteArray, len: i32) -> .i8 {
    $self $arena check-same-arena
    $self.len $len + $self.cap le if {
        $self.ptr $self.len +
        $self.len $len + #self.len
    } else {
        $arena Arena:save @save
        $self.ptr $self.cap + $arena $self.len $len + $self.cap - Arena:alloc-bytes = if {
            // Optimization in case the newly allocated bytes immediately 
            // follow the already allocated bytes of this ByteArray.
            $self.len $len + #self.cap
            $arena $self $len alloc-bytes
        } else {
            $arena $save Arena:restore
            $self.cap 16 CORE:max 2 * #self.cap
            $arena $self.cap Arena:alloc-bytes @new-buf
            $self.ptr $new-buf $self.len CORE:copy-bytes
            $new-buf #self.ptr
            $arena $self $len alloc-bytes
        }
    }
}
fn push-byte(arena: .Arena, self: .ByteArray, byte: i8) {
    $arena $self &byte 1 push
}
fn push-i32(arena: .Arena, self: .ByteArray, v: i32) {
    $arena $self &v !.i8 4 push
}

fn get(self: .ByteArray, index: i32) -> i8 {
    $index $self.len ge if {
        2 "Index out of bounds\n" IO:write-all IO:check
        0 0 / drop
        1 CORE:exit
    }
    $self.ptr $index + ~
}

fn unpack(self: ByteArray) -> .i8, i32 {
    $self.ptr $self.len
}

fn to-slice(self: .ByteArray) -> ByteSlice {
    $self ~ unpack make ByteSlice
}

fn eq(self: .ByteArray, other: .ByteArray) -> bool {
    $self.ptr $self.len $other.ptr $other.len Bytes:eq
}

fn unescape(arena: .Arena:Arena, self: ByteSlice) -> ByteArray {
    $arena $self.len with-capacity @out
    0 @i
    loop {
        $i $self.len = if { break }
        $self.ptr $i + ~ @char
        $char "\\" drop ~ = if {
            $i 1 + $self.len = if {
                $arena &out $char push-byte
                break
            } else {
                $self.ptr $i + 1 + ~ @char
                block {
                    $char "\"" drop ~ = if {
                        $arena &out "\"" drop ~ push-byte break
                    }
                    $char "n" drop ~ = if {
                        $arena &out "\n" drop ~ push-byte break
                    }
                    $char "t" drop ~ = if {
                        $arena &out "\t" drop ~ push-byte break
                    }
                    $char "r" drop ~ = if {
                        $arena &out "\r" drop ~ push-byte break
                    }
                    $char "\\" drop ~ = if {
                        $arena &out "\\" drop ~ push-byte break
                    }
                    0 0 / drop
                }
            }
            $i 2 + #i
        } else {
            $arena &out $char push-byte
            $i 1 + #i
        }
    }
    $out
}

