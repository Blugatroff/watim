import "./core.watim" as CORE
import "./io.watim" as IO
import "./arena.watim" as Arena (Arena)
import "./bytes.watim" as Bytes
import "./byte-slice.watim" as ByteSlice (ByteSlice)

struct ByteArray {
    ptr: .i32
    len: i32
    cap: i32
    arena: .Arena
}

fn new() -> ByteArray {
    make ByteArray {
        0 !.i32 @ptr
        0 @len
        0 @cap
        0 !.Arena @arena
    }
}

fn init(ptr: .i32, len: i32) -> ByteArray {
    make ByteArray {
        $ptr @ptr
        $len @len
        $len @cap
        0 !.Arena @arena
    }
}

fn copy(arena: .Arena, ptr: .i32, len: i32) -> ByteArray {
    new @self
    $arena &self $ptr $len push
    $self
}

fn check-same-arena(self: .ByteArray, arena: .Arena) {
    $self.arena 0 !.Arena = if {
        $arena #self.arena
    } else {
        $self.arena $arena /= if {
            2 "ByteArray: used different arenas\n" IO:write-all IO:check
            0 0 / drop
        }
    }
}

fn push(arena: .Arena, self: .ByteArray, ptr: .i32, len: i32) {
    $ptr $arena $self $len alloc-bytes $len CORE:copy-bytes
}
fn alloc-bytes(arena: .Arena, self: .ByteArray, len: i32) -> .i32 {
    $self $arena check-same-arena
    $self.len $len + $self.cap le if {
        $self.ptr $self.len +
        $self.len $len + #self.len
    } else {
        $arena Arena:save @save
        $self.ptr $self.cap + $arena $self.len $len + $self.cap - Arena:alloc-bytes = if {
            // Optimization in case the newly allocated bytes immediately 
            // follow the already allocated bytes of this ByteArray.
            $self.len $len + #self.cap
            $arena $self $len alloc-bytes
        } else {
            $arena $save Arena:restore
            $self.cap 16 CORE:max 2 * #self.cap
            $arena $self.cap Arena:alloc-bytes @new-buf
            $self.ptr $new-buf $self.len CORE:copy-bytes
            $new-buf #self.ptr
            $arena $self $len alloc-bytes
        }
    }
}
fn push-byte(arena: .Arena, self: .ByteArray, byte: i32) {
    uninit<CORE:I32> @ptr
    $byte #ptr.inner
    $arena $self &ptr.inner 1 push
}
fn push-i32(arena: .Arena, self: .ByteArray, v: i32) {
    uninit<CORE:I32> @ptr
    $v #ptr.inner
    $arena $self &ptr.inner 4 push
}

fn get(self: .ByteArray, index: i32) -> i32 {
    $index $self.len ge if {
        2 "Index out of bounds\n" IO:write-all IO:check
        0 0 / drop
        1 CORE:exit
    }
    $self.ptr $index + load8
}

fn unpack(self: ByteArray) -> .i32, i32 {
    $self.ptr $self.len
}

fn to-slice(self: .ByteArray) -> ByteSlice {
    $self ~ unpack make ByteSlice
}

fn eq(self: .ByteArray, other: .ByteArray) -> bool {
    $self.ptr $self.len $other.ptr $other.len Bytes:eq
}

