import "./core.watim" as CORE
import "./io.watim" as IO
import "./arena.watim" as Arena
import "./bytes.watim" as Bytes

struct ByteSlice {
    ptr: .i32
    len: i32
}
fn unpack(self: ByteSlice) -> .i32, i32 {
    $self.ptr $self.len
}
fn empty() -> ByteSlice {
    0 !.i32 0 make ByteSlice
}
fn copy(arena: .Arena:Arena, self: ByteSlice) -> ByteSlice {
    $arena $self.ptr $self.len Arena:push-bytes $self.len make ByteSlice
}
fn get-byte(self: .ByteSlice, index: i32) -> i32 {
    $index $self.len ge if { 
        1 "ByteArray:get-byte Index out of bounds\n" IO:write-all IO:check
        1 CORE:exit
    }
    $self.ptr $index + load8
}
fn slice(self: .ByteSlice, index: i32, len: i32) -> ByteSlice {
    $index $len + $self.len gt if { 
        1 "ByteArray:slice Index out of bounds\n" IO:write-all IO:check
        1 CORE:exit
    }
    $self.ptr $index + $len make ByteSlice
}
fn eq(self: ByteSlice, other: ByteSlice) -> bool {
    $self unpack $other unpack Bytes:eq
}
fn eq-by-ref(self: .ByteSlice, other: .ByteSlice) -> bool {
    $self ~ unpack $other ~ unpack Bytes:eq
}
fn is-empty(self: .ByteSlice) -> bool {
    $self.len 0 =
}
fn is-not-empty(self: .ByteSlice) -> bool {
    $self.len 0 /=
}
fn drop(self: ByteSlice, n: i32) -> ByteSlice {
    $n $self.len CORE:min #n
    $self.ptr $n +
    $self.len $n -
    make ByteSlice
}
fn drop-end(self: ByteSlice, n: i32) -> ByteSlice {
    $n $self.len CORE:min #n
    $self.ptr
    $self.len $n -
    make ByteSlice
}
fn find-byte(self: ByteSlice, b: i32) -> i32, bool {
    0 @i
    loop {
        $self.len $i = if { 0 not 0 1 = break }
        $self.ptr $i + load8 $b = if {
            $i 1 1 = break
        }
        $i 1 + #i
    }
}
fn find-bytes(self: ByteSlice, ptr: .i32, len: i32) -> i32, bool { block {
    $len 0 = if {
        0 1 1 = break
    }
    0 @i
    loop {
        $i $len + $self.len gt if { 0 not 0 1 = break }
        $self.ptr $i + load8 $ptr load8 = if {
            $self.ptr $i + $len $ptr $len Bytes:eq if {
                $i 1 1 = break
            }
        }
        $i 1 + #i
    }
} }
fn find-slice(self: ByteSlice, slice: ByteSlice) -> i32, bool {
    $self $slice unpack find-bytes
}
fn trim-start-with(self: ByteSlice, f: (i32 -> bool)) -> ByteSlice {
    loop {
        $self.len 0 = if { break }
        $self.ptr load8 $f -> not if { break }
        $self.len 1 - #self.len
        $self.ptr 1 + #self.ptr
    }
    $self
}
fn trim-end-with(self: ByteSlice, f: (i32 -> bool)) -> ByteSlice {
    loop {
        $self.len 0 = if { break }
        $self.ptr $self.len 1 - + load8 $f -> not if { break }
        $self.len 1 - #self.len
    }
    $self
}
fn trim-with(self: ByteSlice, f: (i32 -> bool)) -> ByteSlice {
    $self $f trim-start-with $f trim-end-with
}
fn write-unescaped(fd: i32, self: .ByteSlice) -> i32 {
    $fd $self.ptr $self.len IO:write-all
}
fn unescape(arena: .Arena:Arena, self: ByteSlice) -> ByteSlice {
    // TODO: actually unescape instead of assuming nothing is escaped
    $arena $self copy
}

