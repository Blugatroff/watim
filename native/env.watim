import "../std/maybe.watim" as Maybe (Maybe, assert-some)
import "../std/arena.watim" as Arena (Arena)
import "../std/array.watim" as Array (Array)
import "../std/map.watim" as Map (Map)
import "../std/byte-slice.watim" as ByteSlice (ByteSlice)
import "../std/fmt.watim" as Fmt (Formatter)
import "../std/util.watim" as Util (i32-format, bool-format)
import "../std/bytes.watim" as Bytes
import "../std/io.watim" as IO

import "./lexer.watim" as Lexer (Token, Token-lexeme, Token-format)
import "./resolver/type.watim" as Type (Type, NamedType, Type-format)

struct Local {
    name: Token
    type: Type
    reffed: bool
    is-parameter: bool
}
fn Local-format(fmt: .Formatter, self: .Local) {
    $fmt "(Local " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.type Type-format
    $fmt " " Fmt:write
    $fmt $self.reffed bool-format
    $fmt " " Fmt:write
    $fmt $self.is-parameter bool-format
    $fmt ")" Fmt:write
}

struct LocalId {
    name: ByteSlice
    scope: ScopeId
    shadow: i32
}
fn LocalId-eq(a: .LocalId, b: .LocalId) -> bool {
    $a.scope $b.scope = $a.shadow $b.shadow = and if {
        $a.name.ptr $a.name.len $b.name.ptr $b.name.len Bytes:eq
    } else {
        0 1 =
    }
}
fn LocalId-format(fmt: .Formatter, self: .LocalId) {
    $fmt "(LocalId " Fmt:write
    $fmt &self.name Fmt:ByteSlice-format
    $fmt " " Fmt:write
    $fmt $self.scope.raw i32-format
    $fmt " " Fmt:write
    $fmt $self.shadow i32-format
    $fmt ")" Fmt:write
}

struct ScopeId { raw: i32 }
fn ScopeId-format(fmt: .Formatter, self: .ScopeId) {
    $fmt $self.raw i32-format
}

fn root-scope() -> ScopeId { 0 make ScopeId }

struct Env {
    parent: Maybe<.Env>
    scope-counter: .i32
    scope-id: ScopeId
    vars: Map<ByteSlice, Array<LocalId>>
    vars-by-id: .Map<LocalId, Local>
}
fn new(arena: .Arena) -> Env {
    make Env {
        make Maybe<.Env>.None @parent
        root-scope @scope-id
        $arena 1 Arena:push<i32> @scope-counter
        \ByteSlice:eq-by-ref Map:new<ByteSlice, Array<LocalId>> @vars
        $arena \LocalId-eq Map:new<LocalId, Local> Arena:push<Map<LocalId, Local>> @vars-by-id
    }
}
fn child(self: .Env) -> Env {
    make Env {
        $self make Maybe<.Env>.Some @parent
        $self.scope-counter @scope-counter
        $self.scope-counter ~ make ScopeId @scope-id
        $self.scope-counter ~ 1 + =>self.scope-counter
        \ByteSlice:eq-by-ref Map:new<ByteSlice, Array<LocalId>> @vars
        $self.vars-by-id @vars-by-id
    }
}
fn lookup-by-id(self: .Env, id: .LocalId) -> .Local {
    $self.vars-by-id $id Map:get<LocalId, Local> Maybe:assert-some<.Local>
}
fn lookup(self: .Env, name: .Token) -> Maybe<LocalId> {
    &self.vars $name Token-lexeme Map:get-bs<Array<LocalId>> match {
        case None -> {
            $self.parent match {
                case None -> { make Maybe<LocalId>.None }
                case Some -> { $name lookup }
            }
        }
        case Some -> {
            @vars
            $vars.len 0 = if {
                $self.parent match {
                    case None -> { make Maybe<LocalId>.None }
                    case Some -> { $name lookup }
                }
            } else {
                $vars $vars.len 1 - Array:get<LocalId> ~ make Maybe<LocalId>.Some
            }
        }
    }
}
fn insert(arena: .Arena, self: .Env, var: Local) -> LocalId {
    &var.name Token-lexeme make ByteSlice @name
    &self.vars &name Map:get<ByteSlice, Array<LocalId>> match {
        case None -> {
            $name $self.scope-id 0 make LocalId @id

            $arena &self.vars $name
            $arena $id Array:singleton<LocalId>
            Map:insert<ByteSlice, Array<LocalId>> drop

            $arena $self.vars-by-id $id $var Map:insert<LocalId, Local> drop
            $id
        }
        case Some -> {
            @shadowed
            $name $self.scope-id $shadowed.len make LocalId @id

            $arena $shadowed $id Array:push<LocalId>
            $arena $self.vars-by-id $id $var Map:insert<LocalId, Local> drop
            $id
        }
    }
}
fn insert-parameters(arena: .Arena, self: .Env, params: .Array<NamedType>) {
    0 @i
    loop {
        $i $params.len = if { break }
        $params $i Array:get<NamedType> @parameter
        $arena $self make Local {
            $parameter.type @type
            $parameter.name @name
            0 1 = @reffed
            1 1 = @is-parameter
        } insert drop
        $i 1 + #i
    }
}

