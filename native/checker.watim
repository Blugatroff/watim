import "../std/alloc.watim" as A
import "../std/io.watim" as IO
import "../std/string.watim" as S
import "../std/str.watim" as STR
import "../std/core.watim" as CORE
import "../std/fmt.watim" as FMT
import "../std/dyn-array.watim" as DynArray
import "../std/array.watim" as Array
import "../std/arena.watim" as Arena

import "./module.watim" as M
import "./ast.watim" as AST
import "./lexer.watim" as L
import "./util.watim" as U
import "./break_stack.watim" as BS
import "./intrinsic.watim" as INTR
import "./logging.watim" as LOGGING

struct Checker {
    modules: .M:Modules
    arena: .Arena:Arena
}
fn Checker-new(arena: .Arena:Arena, modules: .M:Modules) -> Checker {
    local self: Checker
    $modules #self.modules
    $arena #self.arena
    $self
}

fn check(arena: .Arena:Arena, modules: .M:Modules) {
    $arena $modules Checker-new @checker
    0 @i
    loop {
        $i $modules M:Modules-len = if { break }
        $modules $i M:Modules-get-value @module
        LOGGING:enabled if {
            2 "Typechecking module: " IO:write-all IO:check
            2 &module.path S:String-unpack IO:write-all IO:check
            2 "\n" IO:write-all IO:check
        }
        &checker $module check-module
        $i 1 + #i
    }
}
fn check-module(self: .Checker, module: .M:Module) {
    $module M:Module-items @items
    0 @i
    loop {
        $i $items.len = if { break }
        $self $module $items $i DynArray:get<.AST:TopItem> ~ check-item
        $i 1 + #i
    }
}
fn check-item(self: .Checker, module: .M:Module, item: .AST:TopItem) {
    $item AST:TopItem-ty AST:TOP_ITEM_FN = if {
        $self $module $item AST:TopItem-function check-function
    }
}

fn report-return-type-mismatch(stack: .AST:Types, function: .AST:Function) {
    S:String-empty @msg
    &msg FMT:Formatter-into-string @formatter
    &formatter "return type mismatch:\nexpected: " FMT:Formatter-write
    &formatter 0 !.AST:Types $function AST:Function-rets AST:Types-format
    &formatter "\ngot:      " FMT:Formatter-write
    &formatter 0 !.AST:Types $stack AST:Types-format
    &formatter "\n" FMT:Formatter-write
    $function AST:Function-ident L:Token-location
    &msg S:String-unpack
    U:abort-with-location
    $msg S:String-free
}
fn check-function(self: .Checker, module: .M:Module, function: .AST:Function) {
    local scope-id: CORE:I32
    0 #scope-id.inner

    $self.arena AST:Types-new @stack

    $scope-id.inner #function.locals.id

    $function AST:Function-body @words
    0!(BS:BreakStack ->) DynArray:new<BS:BreakStack> @bs
    $module &bs $self $stack &function.locals &scope-id.inner $words check-words not if {
        // if the function does not diverge, aka does return
        // then check whether the remaining stack matches the return type
        $stack AST:Types-len $function AST:Function-rets AST:Types-len /= if {
            $stack $function report-return-type-mismatch
        }
        0 @i
        loop {
            $i $function AST:Function-rets AST:Types-len = if { break }
            $stack $stack AST:Types-len 1 - $i - AST:Types-get 
            $function AST:Function-rets $function AST:Function-rets AST:Types-len 1 - $i - AST:Types-get 
            AST:Type-eq not if {
                $stack $function report-return-type-mismatch
            }
            $i 1 + #i
        }
    } else {
        0 0 = #function.diverges
    }

    $bs DynArray:free<BS:BreakStack>
}
fn check-words(
    module: .M:Module,
    bs: .DynArray:DynArray<BS:BreakStack>,
    self: .Checker,
    stack: .AST:Types,
    locals: .AST:Locals,
    scope-id: .i32,
    words: .AST:Words,
) -> bool {
    local i: i32
    local diverges: bool
    loop {
        $i $words AST:Words-len = if { break }
        $diverges
        $module $bs $self $stack $locals $scope-id $words $i AST:Words-get check-word
        or
        #diverges
        $i 1 + #i
    }
    $diverges
}

fn check-word(
    module: .M:Module,
    bs: .DynArray:DynArray<BS:BreakStack>,
    self: .Checker,
    stack: .AST:Types,
    locals: .AST:Locals,
    scope-id: .i32,
    word: .AST:Word
) -> bool {
    loop {
        $word AST:Word-ty AST:WORD_NUMBER = if {
            $self.arena $stack &module.types $self.arena AST:Type-new-int32 U:push-temp-type2 AST:Types-push
            0 1 = break
        }
        $word AST:Word-ty AST:WORD_STRING = if {
            $self.arena $stack &module.types $self.arena &module.types $self.arena AST:Type-new-int32 U:push-temp-type2 AST:Type-new-ptr U:push-temp-type2 AST:Types-push
            $self.arena $stack &module.types $self.arena AST:Type-new-int32 U:push-temp-type2 AST:Types-push
            0 1 = break
        }
        $word AST:Word-ty AST:WORD_CALL = if {
            $self $module $stack $word check-call break
        }
        $word AST:Word-ty AST:WORD_LOOP = if {
            $module $self $stack $locals $scope-id $word check-lop break
        }
        $word AST:Word-ty AST:WORD_BREAK = if {
            $self.arena $bs $stack $word check-brek break
        }
        $word AST:Word-ty AST:WORD_INTRINSIC = if {
            $self.arena &module.types $stack $word INTR:check-intrinsic break
        }
        $word AST:Word-ty AST:WORD_IF = if {
            $module $bs $self $stack $locals $scope-id $word check-iff break
        }
        $word AST:Word-ty AST:WORD_CAST = if {
            $self.arena $stack $word check-cast break
        }
        $word AST:Word-ty AST:WORD_DEREF = if {
            $self.arena &module.types $stack $word check-deref break
        }
        $word AST:Word-ty AST:WORD_SET = if {
            $stack $locals $word check-set break
        }
        $word AST:Word-ty AST:WORD_GET_REF = if {
            $self.arena $stack $locals &module.types $word check-get-ref break
        }
        $word AST:Word-ty AST:WORD_GET_LOCAL = if {
            $self $module $stack $locals $word check-get-locl break
        }
        $word AST:Word-ty AST:WORD_LOAD = if {
            $self.arena $stack $word check-load break
        }
        $word AST:Word-ty AST:WORD_SIZEOF = if {
            $self.arena $stack &module.types $self.arena AST:Type-new-int32 U:push-temp-type2 AST:Types-push
            0 1 = break
        }
        $word AST:Word-ty AST:WORD_STORE = if {
            $stack $self $module $locals $word check-store break
        }
        $word AST:Word-ty AST:WORD_INIT_LOCAL = if {
            $self.arena $stack $locals $word check-init-locl break
        }
        $word AST:Word-ty AST:WORD_ANNOTATION = if {
            $stack $word check-annotation break
        }
        $word AST:Word-ty AST:WORD_ARRAY_WRITE = if {
            $stack $word check-array-write break
        }
        $word AST:Word-ty AST:WORD_ARRAY_READ = if {
            $self.arena $stack $word check-array-read break
        }
        $word AST:Word-ty AST:WORD_ARRAY_REF = if {
            $self.arena $stack $word &module.types check-array-ref break
        }
        $word AST:Word-ty AST:WORD_BLOCK = if {
            $module $self $stack $locals $scope-id $word check-blok break
        }
        $word AST:Word-ty AST:WORD_ARRAY_INIT = if {
            // TODO: this results in miscompilation when the break is missing
            $self.arena $stack $word &module.types check-array-init break
        }
        $word AST:Word-ty AST:WORD_ARRAY_INIT_ZERO = if {
            $stack $word &module.types check-array-init-zero break
        }
        $word AST:Word-ty AST:WORD_CALL_INDIRECT = if {
            $self.arena $stack $word &module.types check-call-indirect break
        }
        $word AST:Word-ty AST:WORD_REF_FUNCTION = if {
            &module.types $self $module $stack $word check-ref-function break
        }
        $word AST:Word-location " unhandled word in checker\n" U:abort-with-location
        0 0 = break
    }
    $word AST:Word-mark-checked
}

fn check-ref-function(temp-types: .DynArray:DynArray<.AST:Type>, self: .Checker, module: .M:Module, stack: .AST:Types, word: .AST:Word) -> bool {
    $self $module $stack $word 1 1 = check-function-usage drop @rets @params
    
    $self.arena
    $stack 

    $temp-types
    $self.arena 
    $params
    $rets AST:Type-new-function 
    U:push-temp-type2

    AST:Types-push

    0 1 =
}

fn check-call-indirect(arena: .Arena:Arena, stack: .AST:Types, word: .AST:Word, temp-types: .DynArray:DynArray<.AST:Type>) -> bool {
    $stack AST:Types-len 0 = if {
        $word AST:Word-location "call indirect expected function pointer\n" U:abort-with-location
    }
    $stack AST:Types-pop @fn-type
    $stack $fn-type.function.args $word U:expect-stack

    $arena 
    $arena $fn-type.function.args AST:Types-clone-container
    $arena $fn-type.function.rets AST:Types-clone-container
    AST:Type-new-function $temp-types flip U:push-temp-type2
    #word.type

    $fn-type.function.rets @rets

    0 @i
    loop {
        $i $rets AST:Types-len = if { break }
        $rets $i AST:Types-get @type
        $arena $stack $type AST:Types-push
        $i 1 + #i
    }
    0 1 =
}

fn check-array-init(arena: .Arena:Arena, stack: .AST:Types, word: .AST:Word, temp-types: .DynArray:DynArray<.AST:Type>) -> bool {
    $word.size @size
    $stack AST:Types-len $size lt if {
        $word AST:Word-location "not enough values on stack to construct array\n" U:abort-with-location
    }
    $size 0 = if {
        $word AST:Word-location "empty arrays not yet supported\n" U:abort-with-location
    }
    $stack AST:Types-pop @elem-type
    $elem-type #word.type

    $size 1 - #size
    loop {
        $size 0 = if { break }
        $stack AST:Types-pop @type
        $type $elem-type AST:Type-eq not if {
            $word AST:Word-location "all elements in array need to be of the same type\n" U:abort-with-location
        }
        $size 1 - #size
    }

    $temp-types $arena $elem-type $word.number AST:Type-new-array U:push-temp-type2
    @array-type

    $arena $stack $array-type AST:Types-push
    $array-type #word.type
    0 1 =
}

fn check-array-init-zero(stack: .AST:Types, word: .AST:Word, temp-types: .DynArray:DynArray<.AST:Type>) -> bool {
    2 "check-array-init-zero TODO\n" IO:write-all IO:check // TODO
    1 CORE:exit
    0 1 =
}

fn check-array-write(stack: .AST:Types, word: .AST:Word) -> bool {
    $stack AST:Types-len 3 lt if {
        $word AST:Word-location "expected at least 3 arguments for array write\n" U:abort-with-location
    }
    $stack AST:Types-pop @type
    $stack AST:Types-pop @index-type
    $index-type AST:Type-ty AST:TYPE_I32 /= if {
        $word AST:Word-location "can only index array with i32\n" U:abort-with-location
    }
    $stack AST:Types-pop @array-type
    $array-type AST:Type-ty AST:TYPE_PTR /= if {
        $word $array-type array-access-mismatch 1 CORE:exit
    }   
    $array-type AST:Type-child AST:Type-child $type AST:Type-eq not if {
        $word $array-type array-access-mismatch 1 CORE:exit
    }
    $array-type AST:Type-child AST:Type-child #word.type
    0 1 =
}

fn array-access-mismatch(word: .AST:Word, array-type: .AST:Type) {
    2 $word AST:Word-location L:print-location
    2 "Type mismatch in array access:" IO:write-all IO:check
    2 "\n\t expected: .[a: n]" IO:write-all IO:check
    2 "\n\t got:      " IO:write-all IO:check
    2 $array-type AST:Type-print
    2 "\n" IO:write-all IO:check
}
fn check-array-read(arena: .Arena:Arena, stack: .AST:Types, word: .AST:Word) -> bool {
    $stack AST:Types-len 2 lt if {
        $word AST:Word-location "expected at least 2 arguments for array read\n" U:abort-with-location
    }
    $stack AST:Types-pop @index-type
    $index-type AST:Type-ty AST:TYPE_I32 /= if {
        $word AST:Word-location "can only read array with i32 index\n" U:abort-with-location
    }
    $stack AST:Types-pop @array-type
    $array-type AST:Type-ty AST:TYPE_PTR /= if {
        $word $array-type array-access-mismatch 1 CORE:exit
    }
    $array-type AST:Type-child AST:Type-ty AST:TYPE_ARRAY /= if {
        $word $array-type array-access-mismatch 1 CORE:exit
    }
    $array-type AST:Type-child AST:Type-child #word.type
    $arena $stack $array-type AST:Type-child AST:Type-child AST:Types-push
    0 1 =
}
fn check-array-ref(arena: .Arena:Arena, stack: .AST:Types, word: .AST:Word, temp-types: .DynArray:DynArray<.AST:Type>) -> bool {
    $stack AST:Types-len 2 lt if {
        $word AST:Word-location "expected at least 2 arguments for array read\n" U:abort-with-location
    }
    $stack AST:Types-pop @index-type
    $index-type AST:Type-ty AST:TYPE_I32 /= if {
        $word AST:Word-location "can only read array with i32 index\n" U:abort-with-location
    }
    $stack AST:Types-pop @array-type
    $array-type AST:Type-ty AST:TYPE_PTR /= if {
        $word $array-type array-access-mismatch 1 CORE:exit
    }
    $array-type AST:Type-child AST:Type-ty AST:TYPE_ARRAY /= if {
        $word $array-type array-access-mismatch 1 CORE:exit
    }
    $arena
    $array-type AST:Type-child AST:Type-child #word.type
    $stack 
    $temp-types
    $arena $array-type AST:Type-child AST:Type-child AST:Type-new-ptr U:push-temp-type2
    AST:Types-push
    0 1 =
}
fn check-annotation(stack: .AST:Types, word: .AST:Word) -> bool {
    $stack AST:Types-len 0 = if {
        $word AST:Word-location "cannot annotate empty stack\n" U:abort-with-location
    }
    $stack AST:Types-last @type
    $type $word.type AST:Type-eq not if {
        2 $word AST:Word-location L:print-location
        2 " Type mismatch with annotation:\n\t" IO:write-all IO:check
        2 $type AST:Type-print
        2 "\n\t" IO:write-all IO:check
        2 $word.type AST:Type-print
        2 "\n" IO:write-all IO:check
        1 CORE:exit
    }
    0 1 =
}
fn check-load(arena: .Arena:Arena, stack: .AST:Types, word: .AST:Word) -> bool {
    local type: .AST:Type
    $stack AST:Types-len 0 = if {
        $word AST:Word-location "expected pointer got []\n" U:abort-with-location
    }
    $stack AST:Types-pop #type
    $type AST:Type-ty AST:TYPE_PTR /= if {
        $word AST:Word-location "expected pointer" U:abort-with-location
    }
    $type AST:Type-child #type
    $arena $stack $type AST:Types-push
    $type #word.type
    0 1 =
}
fn check-locl-fields-access(locals: .AST:Locals, word: .AST:Word) -> .AST:Type, .AST:Type, bool {
    local locl: .AST:Local
    $locals $word AST:Word-ident ~ STR:Str-unpack 0 0 = AST:Locals-find if {
        #locl
        $locl.type
        $locl.type $word check-fields-access 0 0 =
        $locl.shadow-id #word.shadow-id
        $locl.scope-id #word.scope-id
    } else {
        drop
        0 !.AST:Type 0 !.AST:Type 0 1 =
    }
}
fn check-fields-access(src-type: .AST:Type, word: .AST:Word) -> .AST:Type {
    local type: .AST:Type
    local struc: .AST:Struct
    local i: i32
    $src-type #word.src-type
    $src-type #type
    loop {
        $i $word.fields.len = if { break }
        $i 0 = 
        $type AST:Type-ty AST:TYPE_STRUCT = 
        and 
        $type AST:Type-ty AST:TYPE_PTR = if { $type AST:Type-child AST:Type-ty AST:TYPE_STRUCT = } else { 0 1 = }
        or
        not if {
            $word.token L:Token-location "cannot access field of a non-struct type" U:abort-with-location
        }
        // value on the stack is a pointer to a struct
        loop { // loop for every field access which can be reduced to one load with offset
            $i $word.fields.len = if { break }

            &word.fields $i Array:get<.L:Token> ~ @field
            $type AST:Type-ty AST:TYPE_PTR = if {
                $type AST:Type-child #type
            }
            $type AST:Type-struc #struc
            // TODO: move this entire function to code gen instead of type checking
            $struc $field L:Token-lexeme ~ STR:Str-unpack AST:Struct-find-field not if {
                $field L:Token-location "field not found" U:abort-with-location
            }
            AST:Field-type #type
            $i 1 + #i
            $type AST:Type-ty AST:TYPE_STRUCT /= if {
                break
            }
        }
    }
    $type
}
fn check-get-locl(self: .Checker, module: .M:Module, stack: .AST:Types, locals: .AST:Locals, word: .AST:Word) -> bool {
    $locals $word check-locl-fields-access if {
        @type @src-type

        $type AST:Type-ty AST:TYPE_GENERIC = 
        $src-type AST:Type-ty AST:TYPE_STRUCT = 
        and if {
            $src-type.generics $type.generic AST:Types-get #type
        }

        $type #word.type
        $self.arena $stack $type AST:Types-push
    } else {
        drop drop
        $module $word AST:Word-ident ~ STR:Str-unpack M:Module-find-mem if {
            $self.modules $module M:Modules-find-index #word.module
            AST:WORD_GET_GLOBAL #word.ty
            AST:Local-type #word.type
            $self.arena $stack $word.type AST:Types-push
        } else {
            drop
            $word AST:Word-location "local not found\n" U:abort-with-location
        }
    }
    0 1 =
}
fn check-store(stack: .AST:Types, self: .Checker, module: .M:Module, locals: .AST:Locals, word: .AST:Word) -> bool {
    local type: .AST:Type
    local src-type: .AST:Type
    local mem: .AST:Local
    $locals $word check-locl-fields-access if {
        #type #src-type
        $type AST:Type-ty AST:TYPE_PTR =
        $src-type AST:Type-ty AST:TYPE_PTR =
        or not if {
            $word AST:Word-location "expected a pointer for the store instruction\n" U:abort-with-location
        }
        $type AST:Type-child #word.type
        $stack $type AST:Type-child $word U:expect-stack-one
        0 1 =
    } else {
        drop drop
        $module $word AST:Word-ident ~ STR:Str-unpack M:Module-find-mem if {
            #mem
            $self.modules $module M:Modules-find-index #word.module
            $mem AST:Local-type $word check-fields-access #type
            AST:WORD_STORE_GLOBAL #word.ty
            $type AST:Type-child #word.type
            $stack $type AST:Type-child $word U:expect-stack-one
        } else {
            drop
            $word AST:Word-location "local not found\n" U:abort-with-location
        }
        0 1 =
    }
}
fn check-deref(arena: .Arena:Arena, temp-types: .DynArray:DynArray<.AST:Type>, stack: .AST:Types, word: .AST:Word) -> bool {
    $stack AST:Types-len 0 = if {
        $word AST:Word-location "stack is empty\n" U:abort-with-location
    }
    $stack AST:Types-pop #word.src-type
    $word.src-type $word check-fields-access #word.type

    $word.src-type AST:Type-ty AST:TYPE_PTR = if {
        $arena $stack $temp-types $arena $word.type AST:Type-new-ptr U:push-temp-type2 AST:Types-push
    } else {
        $arena $stack $temp-types $arena $word.type AST:Type-new-ptr U:push-temp-type2 AST:Types-push
        // $stack $word.type AST:Types-push
    }
    0 1 =
}
fn check-get-ref(arena: .Arena:Arena, stack: .AST:Types, locals: .AST:Locals, temp-types: .DynArray:DynArray<.AST:Type>, word: .AST:Word) -> bool {
    $locals $word check-locl-fields-access not if {
        $word AST:Word-location "local not found\n" U:abort-with-location
    }
    @type @src-type
    $src-type AST:Type-ty AST:TYPE_STRUCT /= 
    $src-type AST:Type-ty AST:TYPE_ARRAY /= and
    if {
        $src-type AST:Type-ty AST:TYPE_PTR = if {
            $src-type AST:Type-child AST:Type-ty AST:TYPE_STRUCT =
        } else { 0 1 = } not if {
            $word AST:Word-location "cannot get pointer of non-struct local" U:abort-with-location
        }
    }
    $type AST:Type-ty AST:TYPE_GENERIC = 
    $src-type AST:Type-ty AST:TYPE_STRUCT = 
    and if {
        $src-type.generics $type.generic AST:Types-get #type
    }

    $type #word.type
    $arena $stack $temp-types $arena $type AST:Type-new-ptr U:push-temp-type2 AST:Types-push
    0 1 =
}
fn check-init-locl(arena: .Arena:Arena, stack: .AST:Types, locals: .AST:Locals, word: .AST:Word) -> bool {
    local type: .AST:Type
    local shadow-id: i32
    $stack AST:Types-len 0 = if {
        $word AST:Word-location "stack is empty\n" U:abort-with-location
    }
    $stack AST:Types-pop #word.type
    $locals $word.ident ~ STR:Str-unpack 0 1 = AST:Locals-find if {
        @locl
        $locl.shadow-id 1 + #shadow-id
    } else {
        0 #shadow-id
        drop
    }
    $locals.id #word.scope-id
    $shadow-id #word.shadow-id

    $arena $locals 
    AST:LOCAL_LOCAL $word.token $word.type $locals.id $shadow-id AST:Local-new 
    AST:Locals-push

    0 1 =
}
fn check-set(stack: .AST:Types, locals: .AST:Locals, word: .AST:Word) -> bool {
    local type: .AST:Type
    local struc: .AST:Struct
    local src-type: .AST:Type
    $locals $word check-locl-fields-access not if {
        $word AST:Word-location "local not found\n" U:abort-with-location
    }
    #type #src-type
    $type #word.type
    $stack $type $word U:expect-stack-one
    0 1 =
}
fn check-cast(arena: .Arena:Arena, stack: .AST:Types, word: .AST:Word) -> bool {
    $stack AST:Types-len 0 = if { 
        $word AST:Word-location "expected [a] got []\n" U:abort-with-location
    }
    // TODO: don't allow all casts
    $stack $stack AST:Types-len 1 - AST:Types-get #word.src-type
    $stack AST:Types-pop drop

    $arena $stack $word AST:Word-type AST:Types-push
    0 1 =
}
fn check-iff(
    module: .M:Module,
    bs: .DynArray:DynArray<BS:BreakStack>,
    self: .Checker,
    stack: .AST:Types,
    locals: .AST:Locals,
    scope-id: .i32,
    word: .AST:Word
) -> bool {
    local iff: .AST:If
    local params: .AST:Types
    local rets: .AST:Types
    local i: i32

    $self.arena AST:Type-new-boolean @boolean
    $stack $boolean $word U:expect-stack-one

    $self.arena $stack AST:Types-clone-container @iff-stack
    $self.arena $stack AST:Types-clone-container @el-stack

    $iff-stack AST:Types-reset-min
    $el-stack AST:Types-reset-min

    0 1 = @true-branch-diverges
    0 1 = @false-branch-diverges

    $word AST:Word-iff #iff

    $locals #iff.locals.parent
    $scope-id ~ 1 + =>scope-id
    $scope-id ~ #iff.locals.id
    
    $module $bs $self $iff-stack &iff.locals $scope-id $iff AST:If-body check-words #true-branch-diverges

    $iff.has-el if {
        $module $bs $self $el-stack &iff.locals $scope-id $iff AST:If-el check-words #false-branch-diverges
        loop {
            $iff-stack AST:Types-len $el-stack AST:Types-len /= if {
                0 0 = break
            }
            loop {
                $iff-stack AST:Types-len $i = if { 0 1 = break }
                $iff-stack $i AST:Types-get
                $el-stack $i AST:Types-get
                AST:Type-eq not if {
                    0 0 = break
                }
                $i 1 + #i
            } break
        } if {
            2 $word AST:Word-location L:print-location
            2 "Type mismatch in if branches:\n" IO:write-all IO:check
            2 "\tIf  : " IO:write-all IO:check
            2 0 !.AST:Types $iff-stack AST:Types-print
            2 "\n\tElse: " IO:write-all IO:check
            2 0 !.AST:Types $el-stack AST:Types-print
            2 "\n" IO:write-all IO:check
            1 CORE:exit
        }
    }

    $iff-stack AST:Types-min $iff.has-el if { $el-stack AST:Types-min CORE:min } else { } @min
    $self.arena $stack $min $stack AST:Types-len AST:Types-clone-slice #params
    $self.arena $iff-stack $min $iff-stack AST:Types-len AST:Types-clone-slice #rets

    // clear stack
    loop {
        $stack AST:Types-len 0 = if { break }
        $stack AST:Types-pop drop
    }
    // and replace with the iff-stack
    loop {
        $iff-stack AST:Types-len $stack AST:Types-len = if { break }
        $self.arena $stack $iff-stack $stack AST:Types-len AST:Types-get AST:Types-push
    }

    $iff $params AST:If-set-params 
    $rets #iff.rets

    $true-branch-diverges $false-branch-diverges or
}

fn check-brek(arena: .Arena:Arena, bs: .DynArray:DynArray<BS:BreakStack>, stack: .AST:Types, word: .AST:Word) -> bool {
    $bs $word $arena $stack AST:Types-clone-container BS:BreakStack-new DynArray:push<BS:BreakStack>
    loop {
        0 $stack AST:Types-len = if { break }
        $stack AST:Types-pop drop
    }
    0 1 =
}

fn check-lop(
    module: .M:Module,
    self: .Checker,
    stack: .AST:Types,
    locals: .AST:Locals,
    scope-id: .i32,
    word: .AST:Word,
) -> bool {
    0!(BS:BreakStack ->) DynArray:new<BS:BreakStack> @bs

    $locals #word.lop.locals.parent
    $scope-id ~ 1 + =>scope-id
    $scope-id ~ #word.lop.locals.id

    $module &bs $self $stack &word.lop.locals $scope-id $word.lop.body check-words drop
    &bs "loop" check-brek-stacks

    $bs.len 0 = if {
        0 0 = 
        $self.arena AST:Types-new #word.lop.rets
    } else {
        0 @i
        loop {
            &bs 0 DynArray:get<BS:BreakStack> BS:BreakStack-types AST:Types-len $i = if { break }
            $self.arena $stack &bs 0 DynArray:get<BS:BreakStack> BS:BreakStack-types $i AST:Types-get AST:Types-push
            $i 1 + #i
        }
        $self.arena &bs 0 DynArray:get<BS:BreakStack> BS:BreakStack-types AST:Types-clone-container #word.lop.rets
        0 1 =
    }
    $bs DynArray:free<BS:BreakStack>
}

fn check-brek-stacks(bs: .DynArray:DynArray<BS:BreakStack>, name-ptr: .i32, name-len: i32) { 
    block {
    $bs.len 0 = if { break }

    $bs 0 DynArray:get<BS:BreakStack> @first
    
    1 @i
    loop {
        $i $bs.len = if { 0 0 = break }
        $bs $i DynArray:get<BS:BreakStack> BS:BreakStack-types $first BS:BreakStack-types AST:Types-eq not if {
            0 1 = break
        }
        $i 1 + #i
    } not if {
        $bs $name-ptr $name-len report-brek-stack-mismatch
    }
} }

fn report-brek-stack-mismatch(bs: .DynArray:DynArray<BS:BreakStack>, name-ptr: .i32, name-len: i32) {
    2 "type mismatch in " IO:write-all IO:check
    2 $name-ptr $name-len IO:write-all IO:check
    2 ":\n" IO:write-all IO:check
    0 @i
    loop {
        $i $bs.len = if { break }
        $bs $i DynArray:get<BS:BreakStack> @stack
        2 "\t" IO:write-all IO:check
        2 $stack.word AST:Word-location L:print-location
        2 ": " IO:write-all IO:check
        2 0 !.AST:Types $stack.types AST:Types-print
        2 "\n" IO:write-all IO:check
        $i 1 + #i
    }
    1 CORE:exit
}

fn check-blok(
    module: .M:Module,
    self: .Checker,
    stack: .AST:Types,
    locals: .AST:Locals,
    scope-id: .i32,
    word: .AST:Word
) -> bool {
    0!(BS:BreakStack ->) DynArray:new<BS:BreakStack> @bs

    $locals #word.blok.locals.parent
    $scope-id ~ 1 + =>scope-id
    $scope-id ~ #word.blok.locals.id
    
    $stack AST:Types-len @orig-stack-len
    $module &bs $self $stack &word.blok.locals $scope-id $word.blok.body check-words @diverges

    $self.arena AST:Types-new @remaining-types
    $orig-stack-len @i
    loop {
        $i $stack AST:Types-len ge if { break }
        $self.arena $remaining-types $stack $i AST:Types-get AST:Types-push
        $i 1 + #i
    }
    loop {
        $stack AST:Types-len $orig-stack-len le if { break }
        $stack AST:Types-pop drop
    }
    &bs $word $remaining-types BS:BreakStack-new DynArray:push<BS:BreakStack>
    &bs "block" check-brek-stacks

    $bs.len 0 = if {
        $self.arena AST:Types-new #word.blok.rets
    } else {
        0 @i
        loop {
            &bs 0 DynArray:get<BS:BreakStack> BS:BreakStack-types AST:Types-len $i = if { break }
            $self.arena $stack &bs 0 DynArray:get<BS:BreakStack> BS:BreakStack-types $i AST:Types-get AST:Types-push
            $i 1 + #i
        }
        $self.arena &bs 0 DynArray:get<BS:BreakStack> BS:BreakStack-types AST:Types-clone-container #word.blok.rets
    }
    $bs DynArray:free<BS:BreakStack>
    $diverges
}

fn check-function-usage(self: .Checker, module: .M:Module, stack: .AST:Types, word: .AST:Word, mark-indirect: bool) -> .AST:Types, .AST:Types, .M:Module {
    $self.modules $module $word.qualifier $word resolve-module #module
    $module $word AST:Word-ident ~ STR:Str-unpack M:Module-find-function not if {
        drop drop
        "function: `" S:String-new @msg
        &msg $word AST:Word-ident ~ STR:Str-unpack S:String-append
        &msg "` not found\n" S:String-append
        $word AST:Word-location &msg S:String-unpack U:abort-with-location
    } else { 
        #word.function #word.ext
    }

    $word.function !i32 0 /= if {
        $word.function AST:Function-locals
        $word.function AST:Function-rets
        &word.function.generics
        $mark-indirect if {
            1 #word.function.table-index
        }
    } else {
        $word.ext AST:Extern-parameters
        $word.ext AST:Extern-rets
        $mark-indirect if { 
            1 #word.ext.table-index
        }
        0 !.AST:Generics
    } @generics @ret @param-locals

    $word.generics 0 !.AST:Types = if { 0 } else { 
        $word.generics AST:Types-len 
    } @number-generics-call
    $generics 0 !.AST:Generics = if { 0 } else { $generics AST:Generics-len } @number-generics-function
 
    $number-generics-call
    $number-generics-function
    /= if {
        $word AST:Word-location "mismatched number of generics" U:abort-with-location
    }

    $word.generics 0 !.AST:Types /= // there are generic arguments
    $generics 0 !.AST:Generics /=   // the function has generics
    and @generics-involved

    $self.arena AST:Types-new @params
    0 @i
    loop {
        $i $param-locals AST:Locals-len = if { break }
        $param-locals $i AST:Locals-get @locl
        $locl AST:Local-ty AST:LOCAL_ARGUMENT = if {
            $locl AST:Local-type @type
            $self.arena $params $self.arena $type $word.generics $module AST:Type-apply-generics drop AST:Types-push
        }
        $i 1 + #i
    }

    $params $ret $module
}

fn check-call(self: .Checker, module: .M:Module, stack: .AST:Types, word: .AST:Word) -> bool {
    $self $module $stack $word 0 1 = check-function-usage #module @ret @params
    $self.arena $ret AST:Types-clone-container #word.rets
    $self.modules $module M:Modules-find-index #word.module
 
    $stack 
    $params
    $word U:expect-stack
    $params #word.args

    0 @i
    loop {
        $i $ret AST:Types-len = if { break }
        $ret $i AST:Types-get @type
        $self.arena $type $word.generics $module AST:Type-apply-generics if {
            #type
        } else { drop }
        $self.arena $stack $type AST:Types-push
        $i 1 + #i
    }
    0 1 = 
}

fn resolve-module(modules: .M:Modules, module: .M:Module, qualifier: .STR:Str, word: .AST:Word) -> .M:Module {
    $qualifier !i32 0 = if {
        $module
    } else {
        $module M:Module-imports $qualifier ~ STR:Str-unpack AST:Imports-get @b .path @path 
        $b if {
            $modules $path ~ STR:Str-unpack M:Modules-get if {
            } else {
                $word AST:Word-location 
                "failed to find module\n"
                U:abort-with-location
            }
        } else {
            $word AST:Word-location 
            "failed to find import\n"
            U:abort-with-location
            0 !.M:Module
        }
    }
}

