import "../std/core.watim" as Core
import "../std/result.watim" as Result (Result)
import "../std/bytes.watim" as Bytes
import "../std/io.watim" as IO
import "../std/maybe.watim" as Maybe (Maybe, assert-some)
import "../std/byte-slice.watim" as ByteSlice (ByteSlice)
import "../std/byte-array.watim" as ByteArray (ByteArray)
import "../std/array.watim" as Array (Array)
import "../std/map.watim" as Map (Map)
import "../std/arena.watim" as Arena (Arena)
import "../std/fs.watim" as FS
import "../std/util.watim" as Util (i32-format)
import "../std/fmt.watim" as Fmt (Formatter, ByteSlice-format)

import "./logging.watim" as Logging
import "./lexer.watim" as Lexer (Token, Token-lexeme, Token-location, Token-format, Token-print)
import "./parser.watim" as P (TopItem)
import "./type.watim" as Type (Type, Type-eq, Types-eq, Type-format, Type-pretty, NamedType, NamedType-format, CustomType, CustomType-format, ModuleId, TypeLookup, lookup-type, Struct, Variant, VariantCase, CustomTypeHandle, CustomTypeHandle-format, CustomTypeType, CustomTypeType-format, FunType, TupleType, NamedType-type)
import "./env.watim" as Env (Env, new, child, Local, LocalId, LocalId-format, Local-format)

struct FunctionHandle { module: ModuleId index: i32 }
fn FunctionHandle-format(fmt: .Formatter, self: .FunctionHandle) {
    $fmt "(FunctionHandle " Fmt:write
    $fmt $self.module.index i32-format
    $fmt " " Fmt:write
    $fmt $self.index i32-format
    $fmt ")" Fmt:write
}

variant ImportItemItem {
    case Function -> FunctionHandle
    case CustomType -> CustomTypeHandle
}

struct ImportItem {
    name: Token
    handle: ImportItemItem
}
fn ImportItem-format(fmt: .Formatter, self: .ImportItem) {
    $fmt "(ImportItem " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.handle match {
        case Function -> { FunctionHandle-format }
        case CustomType -> { CustomTypeHandle-format }
    }
    $fmt ")" Fmt:write
}

struct Import {
    token: Token
    module: ModuleId
    path: ByteSlice
    qualifier: Token
    items: Array<ImportItem>
}
fn Import-format(fmt: .Formatter, self: .Import) {
    $fmt "(Import " Fmt:write
    $fmt &self.token Token-format
    $fmt " " Fmt:write
    $fmt $self.module.index i32-format
    $fmt " " Fmt:write
    $fmt &self.path ByteSlice-format
    $fmt " " Fmt:write
    $fmt &self.qualifier Token-format
    $fmt " " Fmt:write
    $fmt \ImportItem-format &self.items Array:format-multi-line<ImportItem>
    $fmt ")" Fmt:write
}

struct Global {
    name: Token
    type: Type
}
fn Global-format(fmt: .Formatter, self: .Global) {
    $fmt "(Global " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.type Type-format
    $fmt ")" Fmt:write
}

struct Signature {
    generic-parameters: Array<Token>
    parameters: Array<NamedType>
    returns: Array<Type>
}
fn Signature-format(fmt: .Formatter, self: .Signature) {
    $fmt "(Signature\n" Fmt:write
    $fmt Fmt:indent
    $fmt Fmt:write-indent
    $fmt "generic-parameters=" Fmt:write
    $fmt \Token-format &self.generic-parameters Array:format<Token>
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "parameters=" Fmt:write
    $fmt \NamedType-format &self.parameters Array:format<NamedType>
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "returns=" Fmt:write
    $fmt \Type-format &self.returns Array:format<Type>
    $fmt ")" Fmt:write
    $fmt Fmt:dedent
}

struct Extern {
    name: Token
    extrn-module: ByteSlice
    extrn-name: ByteSlice
    signature: Signature
}
fn Extern-format(fmt: .Formatter, self: .Extern) {
    $fmt "(Extern " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.extrn-module ByteSlice-format
    $fmt " " Fmt:write
    $fmt &self.extrn-name ByteSlice-format
    $fmt " " Fmt:write
    $fmt &self.signature Signature-format
    $fmt ")" Fmt:write
}

struct GlobalId {
    name: Token
    module: ModuleId
    index: i32
}
fn GlobalId-format(fmt: .Formatter, self: .GlobalId) {
    $fmt "(GlobalId " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt $self.module.index i32-format
    $fmt " " Fmt:write
    $fmt $self.index i32-format
    $fmt ")" Fmt:write
}

variant VarId {
    case Local -> LocalId
    case Global -> GlobalId
}
fn VarId-format(fmt: .Formatter, self: .VarId) {
    $self match {
        case Local -> { $fmt flip LocalId-format }
        case Global -> { $fmt flip GlobalId-format }
    }
}

struct FieldAccess {
    name: Token
    source-taip: Type
    target-taip: Type
    field-index: i32
}
fn FieldAccess-format(fmt: .Formatter, self: .FieldAccess) {
    $fmt "(FieldAccess " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.source-taip Type-format
    $fmt " " Fmt:write
    $fmt &self.target-taip Type-format
    $fmt " " Fmt:write
    $fmt $self.field-index i32-format
    $fmt ")" Fmt:write
}

struct InitLocal {
    name: Token
    type: Type
    local: LocalId
}
fn InitLocal-format(fmt: .Formatter, self: .InitLocal) {
    $fmt "(InitLocal " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.type Type-format
    $fmt " " Fmt:write
    $fmt &self.local LocalId-format
    $fmt ")" Fmt:write
}

struct GetLocal {
    name: Token
    var: VarId
    var-type: Type
    fields: Array<FieldAccess>
    result-taip: Type
}
fn GetLocal-format(fmt: .Formatter, self: .GetLocal) {
    $fmt "(GetLocal " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.var VarId-format
    $fmt " " Fmt:write
    $fmt &self.var-type Type-format
    $fmt " " Fmt:write
    $fmt &self.result-taip Type-format
    $fmt " " Fmt:write
    $fmt \FieldAccess-format &self.fields Array:format-multi-line<FieldAccess>
    $fmt ")" Fmt:write
}

struct RefLocal {
    name: Token
    var: VarId
    fields: Array<FieldAccess>
}
fn RefLocal-format(fmt: .Formatter, self: .RefLocal) {
    $fmt "(RefLocal " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.var VarId-format
    $fmt " " Fmt:write
    $fmt \FieldAccess-format &self.fields Array:format-multi-line<FieldAccess>
    $fmt ")" Fmt:write
}

struct StringWord {
    token: Token
    offset: i32
    len: i32
}
fn StringWord-format(fmt: .Formatter, self: .StringWord) {
    $fmt "(StringWord " Fmt:write
    $fmt &self.token Token-format
    $fmt " " Fmt:write
    $fmt $self.offset i32-format
    $fmt " " Fmt:write
    $fmt $self.len i32-format
    $fmt ")" Fmt:write
}

struct Cast {
    token: Token
    src: Type
    dst: Type
}
fn Cast-format(fmt: .Formatter, self: .Cast) {
    $fmt "(Cast " Fmt:write
    $fmt &self.token Token-format
    $fmt " " Fmt:write
    $fmt &self.src Type-format
    $fmt " " Fmt:write
    $fmt &self.dst Type-format
    $fmt ")" Fmt:write
}

struct Sizeof {
    token: Token
    type: Type
}
fn Sizeof-format(fmt: .Formatter, self: .Sizeof) {
    $fmt "(Sizeof " Fmt:write
    $fmt &self.token Token-format
    $fmt " " Fmt:write
    $fmt &self.type Type-format
    $fmt ")" Fmt:write
}

variant Intrinsic {
    case Gt -> Type
    case Lt -> Type
    case Add -> Type
    case Sub -> Type
    case Mul -> Type
    case Div -> Type
    case Mod -> Type
    case Eq -> Type
    case NotEq -> Type
    case MemGrow
    case MemCopy
    case Store8
    case Load8
    case Rotl -> Type
    case And -> Type
    case Or -> Type
    case Ge -> Type
    case Le -> Type
}
fn Intrinsic-format(fmt: .Formatter, self: .Intrinsic) {
    $self match {
        case Gt -> { $fmt "(Gt " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Lt -> { $fmt "(Lt " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Add -> { $fmt "(Add " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Sub -> { $fmt "(Sub " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Mul -> { $fmt "(Mul " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Div -> { $fmt "(Div " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Mod -> { $fmt "(Mod " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Eq -> { $fmt "(Eq " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case NotEq -> { $fmt "(NotEq " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case MemGrow -> { $fmt "MemGrow" Fmt:write }
        case MemCopy -> { $fmt "MemCopy" Fmt:write }
        case Store8 -> { $fmt "Store8" Fmt:write }
        case Load8 -> { $fmt "Load8" Fmt:write }
        case Rotl -> { $fmt "(Rotl " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case And -> { $fmt "(And " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Or -> { $fmt "(Or " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Ge -> { $fmt "(Ge " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
        case Le -> { $fmt "(Le " Fmt:write $fmt flip Type-format $fmt ")" Fmt:write }
    }
}

struct IntrinsicWord {
    token: Token
    intrinsic: Intrinsic
}
fn IntrinsicWord-format(fmt: .Formatter, self: .IntrinsicWord) {
    $fmt "(IntrinsicWord " Fmt:write
    $fmt &self.token Token-format
    $fmt " " Fmt:write
    $fmt &self.intrinsic Intrinsic-format
    $fmt ")" Fmt:write
}

struct Call {
    name: Token
    function: FunctionHandle
    generic-arguments: Array<Type>
}
fn Call-format(fmt: .Formatter, self: .Call) {
    $fmt "(Call " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.function FunctionHandle-format
    $fmt " " Fmt:write
    $fmt \Type-format &self.generic-arguments Array:format<Type>
    $fmt ")" Fmt:write
}

struct FunRef {
    call: Call
}
fn FunRef-format(fmt: .Formatter, self: .FunRef) {
    $fmt "(FunRef " Fmt:write
    $fmt &self.call Call-format
    $fmt ")" Fmt:write
}

struct Uninit {
    token: Token
    type: Type
}
fn Uninit-format(fmt: .Formatter, self: .Uninit) {
    $fmt "(Uninit " Fmt:write
    $fmt &self.token Token-format
    $fmt " " Fmt:write
    $fmt &self.type Type-format
    $fmt ")" Fmt:write
}

struct FieldInit {
    name: Token
    struc: CustomTypeHandle
    generic-arguments: Array<Type>
    type: Type
}
fn FieldInit-format(fmt: .Formatter, self: .FieldInit) {
    $fmt "(FieldInit " Fmt:write
    $fmt &self.name Token-format
    $fmt " " Fmt:write
    $fmt &self.struc CustomTypeHandle-format
    $fmt " " Fmt:write
    $fmt \Type-format &self.generic-arguments Array:format<Type>
    $fmt " " Fmt:write
    $fmt &self.type Type-format
    $fmt ")" Fmt:write
}

struct StructWordNamed {
    token: Token
    type: CustomTypeType
    words: Array<Word>
}
fn StructWordNamed-format(fmt: .Formatter, self: .StructWordNamed) {
    $fmt "(StructWordNamed\n" Fmt:write
    $fmt Fmt:indent
    $fmt Fmt:write-indent
    $fmt "token=" Fmt:write
    $fmt &self.token Token-format
    $fmt ",\n" Fmt:write
    $fmt "type=" Fmt:write
    $fmt &self.type CustomTypeType-format
    $fmt ",\n" Fmt:write
    $fmt "words=" Fmt:write
    $fmt \Word-format &self.words Array:format-multi-line<Word>
    $fmt ")" Fmt:write
    $fmt Fmt:dedent
}

variant Word {
    case Number -> Token
    case GetLocal -> GetLocal
    case RefLocal -> RefLocal
    case InitLocal -> InitLocal
    case String -> StringWord
    case Intrinsic -> IntrinsicWord
    case Cast -> Cast
    case Sizeof -> Sizeof
    case Call -> Call
    case Drop -> Token
    case Flip -> Token
    case Uninit -> Uninit
    case FieldInit -> FieldInit
    case StructWordNamed -> StructWordNamed
    case FunRef -> FunRef
}
fn Word-format(fmt: .Formatter, self: .Word) {
    $self match {
        case Number -> { $fmt "(Number " Fmt:write $fmt flip Token-format $fmt ")" Fmt:write }
        case GetLocal -> { $fmt flip GetLocal-format }
        case RefLocal -> { $fmt flip RefLocal-format }
        case InitLocal -> { $fmt flip InitLocal-format }
        case String -> { $fmt flip StringWord-format }
        case Intrinsic -> { $fmt flip IntrinsicWord-format }
        case Cast -> { $fmt flip Cast-format }
        case Sizeof -> { $fmt flip Sizeof-format }
        case Call -> { $fmt flip Call-format }
        case Drop -> { $fmt "(Drop " Fmt:write $fmt flip Token-format $fmt ")" Fmt:write }
        case Flip -> { $fmt "(Flip " Fmt:write $fmt flip Token-format $fmt ")" Fmt:write }
        case Uninit -> { $fmt flip Uninit-format }
        case FieldInit -> { $fmt flip FieldInit-format }
        case StructWordNamed -> { $fmt flip StructWordNamed-format }
        case FunRef -> { $fmt flip FunRef-format }
    }
}

variant FunctionOrExtern {
    case Extern -> Extern
    case Function -> Function
}
fn FunctionOrExtern-format(fmt: .Formatter, self: .FunctionOrExtern) {
    $fmt $self match {
        case Extern -> { Extern-format }
        case Function -> { Function-format }
    }
}
fn FunctionOrExtern-signature(self: .FunctionOrExtern) -> .Signature {
    $self match {
        case Extern -> { .signature }
        case Function -> { .signature }
    }
}

struct Function {
    name: Token
    export: Maybe<Token>
    signature: Signature
    words: Array<Word>
    locals: Map<LocalId, Local>
}
fn Function-format(fmt: .Formatter, self: .Function) {
    $fmt "(Function\n" Fmt:write
    $fmt Fmt:indent
    $fmt Fmt:write-indent
    $fmt "name=" Fmt:write
    $fmt &self.name Token-format
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "export=" Fmt:write
    $fmt \Token-format &self.export Maybe:format<Token>
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "signature=" Fmt:write
    $fmt &self.signature Signature-format
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "locals=" Fmt:write
    $fmt \LocalId-format \Local-format &self.locals Map:format-multi-line<LocalId, Local>
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "words=" Fmt:write
    $fmt \Word-format &self.words Array:format-multi-line<Word>
    $fmt ")" Fmt:write
}

struct Module {
    imports: Map<ByteSlice, Array<Import>>
    custom-types: Map<ByteSlice, CustomType>
    globals: Map<ByteSlice, Global>
    functions: Map<ByteSlice, FunctionOrExtern>
}
fn format-import-array(fmt: .Formatter, self: .Array<Import>) {
    $fmt \Import-format $self Array:format<Import>
}
fn Module-format(fmt: .Formatter, self: .Module) {
    $fmt "(Module\n" Fmt:write
    $fmt Fmt:indent
    $fmt Fmt:write-indent
    $fmt "imports=" Fmt:write
    $fmt \Fmt:ByteSlice-format \format-import-array &self.imports Map:format-multi-line<ByteSlice, Array<Import>>
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "custom-types=" Fmt:write
    $fmt \ByteSlice-format \CustomType-format &self.custom-types Map:format-multi-line<ByteSlice, CustomType>
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "globals=" Fmt:write
    $fmt \ByteSlice-format \Global-format &self.globals Map:format-multi-line<ByteSlice, Global>
    $fmt ",\n" Fmt:write
    $fmt Fmt:write-indent
    $fmt "functions=" Fmt:write
    $fmt \ByteSlice-format \FunctionOrExtern-format &self.functions Map:format-multi-line<ByteSlice, FunctionOrExtern>
    $fmt ")" Fmt:write
    $fmt Fmt:dedent
}

struct ResolveError {
    module: ModuleId
    line: i32
    column: i32
    message: ByteArray
}
fn ResolveError-print(fd: i32, self: ResolveError, modules: .Map<ByteSlice, Array<TopItem>>) -> i32 { block {
    &modules.keys $self.module.index Array:get<ByteSlice> @file-path
    $fd $file-path ~ ByteSlice:unpack IO:write-all Core:dup<i32> 0 /= if { break } drop
    $fd ":" IO:write-all Core:dup<i32> 0 /= if { break } drop
    $fd $self.line IO:print Core:dup<i32> 0 /= if { break } drop
    $fd ":" IO:write-all Core:dup<i32> 0 /= if { break } drop
    $fd $self.column IO:print Core:dup<i32> 0 /= if { break } drop
    $fd " " IO:write-all Core:dup<i32> 0 /= if { break } drop
    $fd $self.message ByteArray:unpack IO:write-all
} }
fn abort<T>(ctx: .Ctx, token: .Token, message-ptr: .i32, message-len: i32) -> Result<T, ResolveError> {
    $ctx.module-id $token Token-location $ctx.arena $message-ptr $message-len ByteArray:copy make ResolveError
    make Result<T, ResolveError>.Error
}
fn abort-m(ctx: .Ctx, token: .Token, message-ptr: .i32, message-len: i32) -> Maybe<ResolveError> {
    $ctx.module-id $token Token-location $ctx.arena $message-ptr $message-len ByteArray:copy make ResolveError
    make Maybe<ResolveError>.Some
}

struct Ctx {
    arena: .Arena
    parsed-modules: .Map<ByteSlice, Array<TopItem>>
    resolved-modules: .Array<Module>
    top-items: .Array<TopItem>
    module-id: ModuleId
    static-data: ByteArray
}

fn resolve-modules(arena: .Arena, modules: .Map<ByteSlice, Array<TopItem>>) -> Result<Map<ByteSlice, Module>, ResolveError> {
    \ByteSlice:eq-by-ref Map:new<ByteSlice, Module> @resolved-modules
    Array:new<.Array<CustomType>> @other-module-types
    0 @id
    loop {
        $id $modules.keys.len = if { $resolved-modules make Result<Map<ByteSlice, Module>, ResolveError>.Success break }
        &modules.keys $id Array:get<ByteSlice> @module-path
        &modules.values $id Array:get<Array<TopItem>> @top-items

        Logging:enabled if {
            2 "Checking module: " IO:write-all IO:check
            2 $module-path ~ ByteSlice:unpack IO:write-all IO:check
            2 "\n" IO:write-all IO:check
        }

        $arena $modules &resolved-modules.values $top-items $id make ModuleId ByteArray:new make Ctx @ctx
        &ctx resolve-imports match {
            case Error -> { make Result<Map<ByteSlice, Module>, ResolveError>.Error break }
            case Success -> {}
        } @imports
        &ctx &imports resolve-custom-types match {
            case Error -> { make Result<Map<ByteSlice, Module>, ResolveError>.Error break }
            case Success -> {}
        } @custom-types
        &ctx &imports resolve-globals match {
            case Error -> { make Result<Map<ByteSlice, Module>, ResolveError>.Error break }
            case Success -> {}
        } @globals
        &ctx &imports resolve-signatures match {
            case Error -> { make Result<Map<ByteSlice, Module>, ResolveError>.Error break }
            case Success -> {}
        } @signatures

        make TypeLookup {
            $ctx.module-id @module-id
            &custom-types.values @types
            &other-module-types @other-modules
        } @type-lookup
        &ctx &imports &type-lookup &signatures &globals resolve-functions match {
            case Error -> { make Result<Map<ByteSlice, Module>, ResolveError>.Error break }
            case Success -> {}
        } @functions

        $arena &resolved-modules $module-path ~
        $imports $custom-types $globals $functions make Module
        Map:insert<ByteSlice, Module> drop
        $arena &other-module-types &resolved-modules.values $id Array:get<Module> .custom-types.values Array:push<.Array<CustomType>>
        $id 1 + #id
    }
}

fn resolve-globals(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>) -> Result<Map<ByteSlice, Global>, ResolveError> {
    \ByteSlice:eq-by-ref Map:new<ByteSlice, Global> @globals
    0 @i
    loop () -> Result<Map<ByteSlice, Global>, ResolveError> {
        $ctx.top-items.len $i = if { $globals make Result<Map<ByteSlice, Global>, ResolveError>.Success break }
        $ctx.top-items $i Array:get<TopItem> match {
            case Global -> {
                @globl
                $ctx.arena &globals &globl.name Token-lexeme make ByteSlice
                $globl.name
                $ctx $imports &globl.type resolve-type match {
                    case Error -> { make Result<Map<ByteSlice, Global>, ResolveError>.Error break }
                    case Success -> {}
                }
                make Global
                Map:insert<ByteSlice, Global> drop
            }
            case _ -> { drop }
        }
        $i 1 + #i
    }
}

fn resolve-functions(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, type-lookup: .TypeLookup, signatures: .Array<Signature>, globals: .Map<ByteSlice, Global>) -> Result<Map<ByteSlice, FunctionOrExtern>, ResolveError> {
    \ByteSlice:eq-by-ref Map:new<ByteSlice, FunctionOrExtern> @functions
    0 @i
    loop () -> Result<Map<ByteSlice, FunctionOrExtern>, ResolveError> {
        $ctx.top-items.len $i = if { $functions make Result<Map<ByteSlice, FunctionOrExtern>, ResolveError>.Success break }
        $ctx.top-items $i Array:get<TopItem> match {
            case Function -> {
                @fun
                $ctx.arena &functions &fun.name Token-lexeme make ByteSlice
                $fun.name $fun.export
                $ctx $imports &fun.signature resolve-signature match {
                    case Error -> { make Result<Map<ByteSlice, FunctionOrExtern>, ResolveError>.Error break }
                    case Success -> {}
                } @signature
                $ctx.arena Env:new @env
                $ctx.arena &env &signature.parameters Env:insert-parameters
                0 @i
                Stack-new-root @stack
                $ctx $imports $env $type-lookup $signatures $globals WordCtx-new @ctx
                $signature
                &ctx &stack &fun.body.words resolve-words match {
                    case Error -> { make Result<Map<ByteSlice, FunctionOrExtern>, ResolveError>.Error break }
                    case Success -> {}
                } [,] @diverges
                &stack.positive &signature.returns Types-eq not if {
                    $ctx.arena Fmt:into-new-string @msg @fmt
                    &fmt "unexpected return values:\n\texpected: " Fmt:write
                    &fmt \Type-format &signature.returns Array:format<Type>
                    &fmt "\n\tactual:   " Fmt:write
                    &fmt \Type-format &stack.positive Array:format<Type>
                    $ctx.ctx &fun.name $msg ~ ByteArray:unpack abort<Map<ByteSlice, FunctionOrExtern>> break
                }
                $env.vars-by-id ~
                make Function
                make FunctionOrExtern.Function
                Map:insert<ByteSlice, FunctionOrExtern> drop
            }
            case Extern -> {
                @extrn
                $ctx.arena
                &functions
                &extrn.name Token-lexeme make ByteSlice
                $extrn.name
                &extrn.extrn-module Token-lexeme 2 - flip 1 + flip make ByteSlice
                &extrn.extrn-name Token-lexeme 2 - flip 1 + flip make ByteSlice
                $ctx $imports &extrn.signature resolve-signature match {
                    case Error -> { make Result<Map<ByteSlice, FunctionOrExtern>, ResolveError>.Error break }
                    case Success -> {}
                }
                make Extern make FunctionOrExtern.Extern
                Map:insert<ByteSlice, FunctionOrExtern> drop
            }
            case _ -> { drop }
        }
        $i 1 + #i
    }
}

struct StructLitCtx {
    struc: CustomTypeHandle
    generic-arguments: Array<Type>
    fields: Map<ByteSlice, Type>
}

struct WordCtx {
    arena: .Arena
    ctx: .Ctx
    imports: .Map<ByteSlice, Array<Import>>
    env: Env
    type-lookup: .TypeLookup
    ptr-I32: .Type
    I32: .Type
    signatures: .Array<Signature>
    globals: .Map<ByteSlice, Global>
    struc-lit-ctx: Maybe<.StructLitCtx>
}
fn WordCtx-new(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, env: Env, type-lookup: .TypeLookup, signatures: .Array<Signature>, globals: .Map<ByteSlice, Global>) -> WordCtx {
    make WordCtx {
        $ctx.arena @arena
        $ctx @ctx
        $imports @imports
        $env @env
        $type-lookup @type-lookup
        $ctx.arena make Type.I32 Arena:push<Type> @i32_
        $i32_ @I32
        $ctx.arena $i32_ make Type.Ptr Arena:push<Type> @ptr-I32
        $signatures @signatures
        $globals @globals
        make Maybe<.StructLitCtx>.None @struc-lit-ctx
    }
}
fn with-struc-lit-ctx(ctx: .WordCtx, struc-lit-ctx: .StructLitCtx) -> WordCtx {
    $ctx ~ @new
    $struc-lit-ctx make Maybe<.StructLitCtx>.Some #new.struc-lit-ctx
    $new
}

fn lookup-signature(ctx: .WordCtx, function: .FunctionHandle) -> .Signature {
    $function.module $ctx.ctx.module-id = if {
        $ctx.signatures $function.index Array:get<Signature>
    } else {
        $ctx.ctx.resolved-modules $function.module.index Array:get<Module> @module
        &module.functions.values $function.index Array:get<FunctionOrExtern> FunctionOrExtern-signature
    }
}

fn resolve-words(ctx: .WordCtx, stack: .Stack, words: .Array<P:Word>) -> Result<[Array<Word>, bool], ResolveError> {
    0 1 = @diverges
    Array:new<Word> @resolved
    0 @i
    loop () -> Result<[Array<Word>, bool], ResolveError> {
        $words.len $i = if { $resolved $diverges [,2] make Result<[Array<Word>, bool], ResolveError>.Success break }
        $ctx.arena &resolved
        $ctx $stack $words $i Array:get<P:Word> resolve-word match {
            case Error -> { make Result<[Array<Word>, bool], ResolveError>.Error break }
            case Success -> {}
        } [,] $diverges or #diverges
        Array:push<Word>
        $i 1 + #i
    }
}

fn resolve-word(ctx: .WordCtx, stack: .Stack, word: .P:Word) -> Result<[Word, bool], ResolveError> { block {
    $word match {
        case Number -> {
            $ctx $stack make Type.I32 push
            ~ make Word.Number 0 1 = [,2]
        }
        case String -> {
            @token
            $ctx $stack $ctx.arena make Type.I32 Arena:push<Type> make Type.Ptr push
            $ctx $stack make Type.I32 push
            $token ~
            $ctx.ctx $token Token-lexeme allocate-static-data
            make StringWord
            make Word.String 0 1 = [,2]
        }
        case GetLocal -> { $ctx flip $stack flip resolve-get-local break }
        case RefLocal -> { $ctx flip $stack flip resolve-ref-local break }
        case InitLocal -> { $ctx flip $stack flip resolve-init-local break }
        case Call -> { $ctx flip $stack flip resolve-call break }
        case Cast -> { $ctx flip $stack flip resolve-cast break }
        case Sizeof -> { $ctx flip $stack flip resolve-sizeof break }
        case MakeStruct -> { $ctx flip $stack flip resolve-mk-struc break }
        case MakeStructNamed -> { $ctx flip $stack flip resolve-mk-struc-named break }
        case FunRef -> { $ctx flip $stack flip resolve-fun-ref break }
        case _ -> { drop make Lexer:TokenTy.TOKEN_MAKE 0 0 Lexer:Token-new-keyword make Word.Number 0 1 = [,2] }
    } make Result<[Word, bool], ResolveError>.Success
} }

fn allocate-static-data(ctx: .Ctx, ptr: .i32, len: i32) -> i32, i32 {
    $ctx.static-data.len
    $ctx.arena &ctx.static-data $ptr $len ByteArray:push
    $len
}

struct Stack {
    parent: Maybe<.Stack>
    positive: Array<Type>
    negative: Array<Type>
}
fn Stack-new-root() -> Stack {
    make Maybe<.Stack>.None Array:new<Type> Array:new<Type> make Stack
}
fn Stack-clone(arena: .Arena, self: .Stack) -> Stack {
    $self.parent match {
        case None -> { make Maybe<.Stack>.None }
        case Some -> { $arena flip Stack-clone $arena flip Arena:push<Stack> make Maybe<.Stack>.Some }
    }
    $arena &self.positive Array:clone<Type>
    $arena &self.negative Array:clone<Type>
    make Stack
}
fn push(ctx: .WordCtx, self: .Stack, type: Type) {
    $ctx.arena &self.positive $type Array:push<Type>
}
fn push-many(ctx: .WordCtx, self: .Stack, types: .Array<Type>) {
    0 @i
    loop {
        $i $types.len = if { break }
        $ctx $self $types $i Array:get<Type> ~ push
        $i 1 + #i
    }
}
fn pop(ctx: .WordCtx, self: .Stack) -> Maybe<Type> {
    $self.positive.len 0 = if {
        $self.parent match {
            case None -> { make Maybe<Type>.None }
            case Some -> {
                $ctx flip pop match {
                    case None -> { make Maybe<Type>.None }
                    case Some -> {
                        @type
                        $ctx.arena &self.negative $type Array:push<Type>
                        $type make Maybe<Type>.Some
                    }
                }
            }
        }
    } else {
        &self.positive Array:pop<Type> make Maybe<Type>.Some
    }
}
fn child(ctx: .WordCtx, self: .Stack) -> Stack {
    $ctx.arena $ctx.arena $self Stack-clone Arena:push<Stack> make Maybe<.Stack>.Some
    Array:new<Type>
    Array:new<Type>
    make Stack
}
fn use(ctx: .WordCtx, self: .Stack, n: i32) -> bool {
    $ctx.arena Arena:temp @temp @save
    Array:new<Type> @popped
    loop () -> bool {
        $popped.len $n = if { 1 1 = break }
        $temp &popped $ctx $self pop match {
            case None -> { 0 1 = break }
            case Some -> {}
        } Array:push<Type>
    }
    &popped Array:reverse<Type>
    loop () -> {
        $popped.len 0 = if { break }
        $ctx.arena &self.positive
        &popped Array:pop<Type>
        Array:push<Type>
    }
    $temp $save Arena:restore
}

fn resolve-fun-ref(ctx: .WordCtx, stack: .Stack, word: .P:FunRef) -> Result<[Word, bool], ResolveError> { block {
    $ctx &word.function resolve-call-word match {
        case Error -> { make Result<[Word, bool], ResolveError>.Error break }
        case Success -> {}
    } @word
    $ctx &word.function lookup-signature @signature
    $ctx.arena &signature.parameters \NamedType-type Array:map<NamedType, Type> @parameters
    $ctx $stack $word.name $parameters $signature.returns make FunType make Type.Fun push
    $word make FunRef
    make Word.FunRef 0 1 = [,2]
    make Result<[Word, bool], ResolveError>.Success
} }

fn resolve-mk-struc(ctx: .WordCtx, stack: .Stack, word: .P:MakeStruct) -> Result<[Word, bool], ResolveError> { block {
    $ctx.ctx &word.token "LKSJDFTODO" abort<[Word, bool]>
} }

fn resolve-mk-struc-named(ctx: .WordCtx, stack: .Stack, word: .P:MakeStructNamed) -> Result<[Word, bool], ResolveError> { block {
    $ctx.ctx $ctx.imports &word.type resolve-custom-type match {
        case Error -> { make Result<[Word, bool], ResolveError>.Error break }
        case Success -> {}
    } @struct-type
    $ctx.type-lookup $struct-type.type-definition lookup-type match {
        case Struct -> {}
        case Variant -> {
            drop
            $ctx.ctx &word.token "can not make this type" abort<[Word, bool]> break
        }
    } @struc
    \ByteSlice:eq-by-ref Map:new<ByteSlice, Type> @fields
    0 @i
    loop {
        $struc.fields.len $i = if { break }
        &struc.fields $i Array:get<NamedType> @field
        $ctx.arena &fields &field.name Token-lexeme make ByteSlice $field.type Map:insert<ByteSlice, Type> drop
        $i 1 + #i
    }
    make StructLitCtx {
        $struct-type.type-definition @struc
        $struct-type.generic-arguments @generic-arguments
        $fields @fields
    } @struc-lit-ctx
    $ctx &struc-lit-ctx with-struc-lit-ctx @child-ctx
    &child-ctx $stack &word.body.words resolve-words match {
        case Error -> { make Result<[Word, bool], ResolveError>.Error break }
        case Success -> {}
    } [,] @diverges @body
    $struc-lit-ctx.fields.values.len 0 /= if {
        $ctx.ctx &word.token "missing field in struct literal" abort<[Word, bool]> break
    }
    $ctx $stack $struct-type make Type.Custom push

    $word.token $struct-type $body make StructWordNamed make Word.StructWordNamed
    $diverges [,2]
    make Result<[Word, bool], ResolveError>.Success
} }

fn resolve-sizeof(ctx: .WordCtx, stack: .Stack, word: .P:Sizeof) -> Result<[Word, bool], ResolveError> { block () -> Result<[Word, bool], ResolveError> {
    $ctx $stack make Type.I32 push
    $word.token
    $ctx.ctx $ctx.imports &word.type resolve-type match {
        case Error -> { make Result<[Word, bool], ResolveError>.Error break }
        case Success -> {}
    }
    make Sizeof
    make Word.Sizeof 0 1 = [,2]
    make Result<[Word, bool], ResolveError>.Success
} }

fn resolve-cast(ctx: .WordCtx, stack: .Stack, word: .P:Cast) -> Result<[Word, bool], ResolveError> { block {
    $ctx $stack pop match {
        case None -> { $ctx.ctx &word.token "cast expected a value, got []" abort<[Word, bool]> break }
        case Some -> {}
    } @src
    $ctx.ctx $ctx.imports &word.type resolve-type match {
        case Error -> { make Result<[Word, bool], ResolveError>.Error break }
        case Success -> {}
    } @dst
    $ctx $stack $dst push
    $word.token $src $dst make Cast
    make Word.Cast 0 1 = [,2]
    make Result<[Word, bool], ResolveError>.Success
} }

fn resolve-call(ctx: .WordCtx, stack: .Stack, parsed-word: .P:Call) -> Result<[Word, bool], ResolveError> { block {
    $parsed-word match {
        case Local -> {
            @word
            &word.name Token-lexeme @name-len @name-ptr
            $name-ptr $name-len "gt" Bytes:eq
            $name-ptr $name-len "lt" Bytes:eq or
            $name-ptr $name-len "ge" Bytes:eq or
            $name-ptr $name-len "le" Bytes:eq or
            $name-ptr $name-len "%" Bytes:eq or
            $name-ptr $name-len "*" Bytes:eq or
            $name-ptr $name-len "/" Bytes:eq or
            if {
                $ctx $stack pop match {
                    case None -> {
                        "`" ByteArray:init @msg
                        $ctx.arena &msg $name-ptr $name-len ByteArray:push
                        $ctx.arena &msg "` expected two values on the stack, got []" ByteArray:push
                        $ctx.ctx &word.name $msg ByteArray:unpack abort<[Word, bool]> break
                    }
                    case Some -> {}
                } @a
                $ctx $stack pop match {
                    case None -> {
                        $ctx.arena Fmt:into-new-string @msg @fmt
                        &fmt "`" Fmt:write
                        &fmt $name-ptr $name-len Fmt:write
                        &fmt "` expected two values on the stack, got [" Fmt:write
                        &fmt $ctx.type-lookup &a Type-pretty
                        &fmt "]" Fmt:write
                        $ctx.ctx &word.name $msg ~ ByteArray:unpack abort<[Word, bool]> break
                    }
                    case Some -> {}
                } @b
                &a &b Type-eq not if {
                    $ctx.arena Fmt:into-new-string @msg @fmt
                    &fmt "`" Fmt:write
                    &fmt $name-ptr $name-len Fmt:write
                    &fmt "` expected two values of the same type, got [" Fmt:write
                    &fmt $ctx.type-lookup &a Type-pretty
                    &fmt ", " Fmt:write
                    &fmt $ctx.type-lookup &b Type-pretty
                    &fmt "]" Fmt:write
                    $ctx.ctx &word.name $msg ~ ByteArray:unpack abort<[Word, bool]> break
                }
                $a match {
                    case I32 -> {}
                    case I64 -> {}
                    case _ -> {
                        drop
                        $ctx.arena Fmt:into-new-string @msg @fmt
                        &fmt "`" Fmt:write
                        &fmt $name-ptr $name-len Fmt:write
                        &fmt "` expected [i32, i32] or [i64, i64], got [" Fmt:write
                        &fmt $ctx.type-lookup &a Type-pretty
                        &fmt ", " Fmt:write
                        &fmt $ctx.type-lookup &b Type-pretty
                        &fmt "]" Fmt:write
                        $ctx.ctx &word.name $msg ~ ByteArray:unpack abort<[Word, bool]> break
                    }
                }
                $ctx $stack $a push
                $a
                block {
                    $name-ptr $name-len "gt" Bytes:eq if { make Intrinsic.Gt break }
                    $name-ptr $name-len "lt" Bytes:eq if { make Intrinsic.Lt break }
                    $name-ptr $name-len "ge" Bytes:eq if { make Intrinsic.Ge break }
                    $name-ptr $name-len "le" Bytes:eq if { make Intrinsic.Le break }
                    $name-ptr $name-len "%"  Bytes:eq if { make Intrinsic.Mod break }
                    make Intrinsic.Mul
                }
                $word.name flip make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "-" Bytes:eq
            $name-ptr $name-len "+" Bytes:eq or
            if {
                $ctx $stack pop match {
                    case None -> {
                        "`" ByteArray:init @msg
                        $ctx.arena &msg $name-ptr $name-len ByteArray:push
                        $ctx.arena &msg "` expected two values on the stack, got []" ByteArray:push
                        $ctx.ctx &word.name $msg ByteArray:unpack abort<[Word, bool]> break
                    }
                    case Some -> {}
                } @b
                $ctx $stack pop match {
                    case None -> {
                        $ctx.arena Fmt:into-new-string @msg @fmt
                        &fmt "`" Fmt:write
                        &fmt $name-ptr $name-len Fmt:write
                        &fmt "` expected two values on the stack, got [" Fmt:write
                        &fmt $ctx.type-lookup &b Type-pretty
                        &fmt "]" Fmt:write
                        $ctx.ctx &word.name $msg ~ ByteArray:unpack abort<[Word, bool]> break
                    }
                    case Some -> {}
                } @a
                &a match {
                    case I32 -> { &a &b Type-eq }
                    case I64 -> { &a &b Type-eq }
                    case Ptr -> { drop &b match {
                        case I32 -> { 1 1 = }
                        case _ -> { drop 0 1 = }
                    } }
                    case _ -> { drop 0 1 = }
                } not if {
                    $ctx.arena Fmt:into-new-string @msg @fmt
                    &fmt "`" Fmt:write
                    &fmt $name-ptr $name-len Fmt:write
                    &fmt "` expected [i32, i32], [.{any}, i32] or [i64, i64], got [" Fmt:write
                    &fmt $ctx.type-lookup &a Type-pretty
                    &fmt ", " Fmt:write
                    &fmt $ctx.type-lookup &b Type-pretty
                    &fmt "]" Fmt:write
                    $ctx.ctx &word.name $msg ~ ByteArray:unpack abort<[Word, bool]> break
                }
                $ctx $stack $b push
                block {
                    $name-ptr $name-len "-" Bytes:eq if {
                        $b make Intrinsic.Sub break
                    }
                    $b make Intrinsic.Add break
                }
                $word.name flip make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "mem-copy" Bytes:eq if {
                $ctx $stack &word.name $ctx.ptr-I32 $ctx.ptr-I32 $ctx.I32 "`mem-copy` expected [.i32, .i32, i32]" expect3 match {
                    case Some -> { make Result<[Word, bool], ResolveError>.Error break }
                    case None -> {}
                }
                $word.name make Intrinsic.MemCopy make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "mem-grow" Bytes:eq if {
                $ctx $stack &word.name $ctx.I32 "`mem-grow` expected [i32]" expect1 match {
                    case Some -> { make Result<[Word, bool], ResolveError>.Error break }
                    case None -> {}
                }
                $ctx $stack make Type.I32 push
                make Intrinsic.MemGrow
                $word.name flip make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "=" Bytes:eq
            $name-ptr $name-len "/=" Bytes:eq or if {
                $ctx $stack &word.name "`=` expected [a, a]" pop2 match {
                    case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                    case Success -> {}
                } [,] @a @b
                &a &b Type-eq not if {
                    $ctx.ctx &word.name "cannot compare two different types" abort<[Word, bool]> break
                }
                $ctx $stack make Type.Bool push
                $word.name
                $a $name-ptr load8 "=" drop load8 = if { make Intrinsic.Eq } else { make Intrinsic.NotEq }
                make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "drop" Bytes:eq if {
                $ctx $stack &word.name "drop expected a value" pop1 match {
                    case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                    case Success -> {}
                } drop
                $word.name make Word.Drop 0 1 = [,2] make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "flip" Bytes:eq if {
                $ctx $stack &word.name "drop expected two values" pop2 match {
                    case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                    case Success -> {}
                } [,]
                $ctx flip $stack flip push
                $ctx flip $stack flip push
                $word.name make Word.Flip 0 1 = [,2] make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "load8" Bytes:eq if {
                $ctx $stack &word.name $ctx.ptr-I32 "`load8` expected [.i32]" expect1 match {
                    case Some -> { make Result<[Word, bool], ResolveError>.Error break }
                    case None -> {}
                }
                $word.name make Intrinsic.Load8 make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "store8" Bytes:eq if {
                $ctx $stack &word.name $ctx.ptr-I32 $ctx.I32 "`store8` expected [.i32, i32]" expect2 match {
                    case Some -> { make Result<[Word, bool], ResolveError>.Error break }
                    case None -> {}
                }
                $word.name make Intrinsic.Store8 make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "and" Bytes:eq if {
                "`and` expected [bool, bool] or [i32, i32] or [i64, i64]" @msg-len @msg-ptr
                $ctx $stack &word.name $msg-ptr $msg-len pop2 match {
                    case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                    case Success -> {}
                } [,] @a @b
                &a &b Type-eq not if { $ctx.ctx &word.name $msg-ptr $msg-len abort<[Word, bool]> break }
                &a match {
                    case Bool -> {}
                    case I32 -> {}
                    case I64 -> {}
                    case _ -> { drop $ctx.ctx &word.name $msg-ptr $msg-len abort<[Word, bool]> break }
                }
                $ctx $stack $a push
                $word.name $a make Intrinsic.And make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "or" Bytes:eq if {
                "`or` expected [bool, bool] or [i32, i32] or [i64, i64]" @msg-len @msg-ptr
                $ctx $stack &word.name $msg-ptr $msg-len pop2 match {
                    case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                    case Success -> {}
                } [,] @a @b
                &a &b Type-eq not if { $ctx.ctx &word.name $msg-ptr $msg-len abort<[Word, bool]> break }
                &a match {
                    case Bool -> {}
                    case I32 -> {}
                    case I64 -> {}
                    case _ -> { drop $ctx.ctx &word.name $msg-ptr $msg-len abort<[Word, bool]> break }
                }
                $ctx $stack $a push
                $word.name $a make Intrinsic.Or make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }
            $name-ptr $name-len "rotl" Bytes:eq if {
                $ctx $stack &word.name "`rotl` expected [i32 | i64, i32]" pop2 match {
                    case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                    case Success -> {}
                } [,] @shift @value
                $value match {
                    case I32 -> {}
                    case I64 -> {}
                    case _ -> {
                        drop
                        $ctx.arena Fmt:into-new-string @msg @fmt
                        &fmt "`rotl` expected [i32 | i64, i32]" Fmt:write
                        &fmt ", got: [" Fmt:write
                        &fmt $ctx.type-lookup &value Type-pretty
                        &fmt ", " Fmt:write
                        &fmt $ctx.type-lookup &shift Type-pretty
                        &fmt "]" Fmt:write
                        $ctx.ctx &word.name $msg ~ ByteArray:unpack abort<[Word, bool]> break
                    }
                }
                $ctx $stack $value push
                $word.name $value make Intrinsic.Rotl make IntrinsicWord
                make Word.Intrinsic 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }

            $name-ptr $name-len "uninit" Bytes:eq if {
                $ctx.ctx $ctx.imports &word.generic-arguments resolve-types match {
                    case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                    case Success -> {}
                } @resolved-generic-arguments
                $resolved-generic-arguments.len 1 /= if {
                    $ctx.ctx &word.name "uninit expected exactly one generic arguments" abort<[Word, bool]> break
                }
                $word.name &resolved-generic-arguments 0 Array:get<Type> ~ Core:dup<Type>
                $ctx flip $stack flip push
                make Uninit
                make Word.Uninit 0 1 = [,2]
                make Result<[Word, bool], ResolveError>.Success break
            }

            $ctx $parsed-word resolve-call-word match {
                case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                case Success -> {}
            } @word
            $ctx &word.function lookup-signature @signature
            $ctx $stack &word.name &signature.parameters expect-arguments match {
                case None -> {}
                case Some -> { make Result<[Word, bool], ResolveError>.Error break }
            }
            $ctx $stack &signature.returns push-many

            $word make Word.Call 0 1 = [,2]
            make Result<[Word, bool], ResolveError>.Success break
        }
        case Foreign -> {
            @word
            $ctx.ctx $ctx.imports &word.generic-arguments resolve-types match {
                case Error -> { make Result<[Word, bool], ResolveError>.Error break }
                case Success -> {}
            } @resolved-generic-arguments
            $ctx.imports &word.module Token-lexeme Map:get-bs<Array<Import>> match {
                case None -> { $ctx.ctx &word.name "module not found" abort<[Word, bool]> break }
                case Some -> {}
            } @imports
            0 @i
            loop {
                $imports.len $i = if {
                    $ctx.ctx &word.name "function not found" abort<[Word, bool]> break
                }
                $imports $i Array:get<Import> @imp
                $ctx.ctx.resolved-modules $imp.module.index Array:get<Module> @module
                &module.functions &word.name Token-lexeme Map:get-bs-with-index<FunctionOrExtern> match {
                    case None -> {}
                    case Some -> {
                        [,] @function-id @function
                        $function FunctionOrExtern-signature @signature
                        $signature.generic-parameters.len $resolved-generic-arguments.len /= if {
                            $ctx.ctx &word.name "mismatched number of generic arguments" abort<[Word, bool]> break
                        }
                        $ctx $stack &word.name &signature.parameters expect-arguments match {
                            case None -> {}
                            case Some -> { make Result<[Word, bool], ResolveError>.Error break }
                        }
                        $ctx $stack &signature.returns push-many

                        $word.name
                        $imp.module $function-id make FunctionHandle
                        $resolved-generic-arguments
                        make Call
                        make Word.Call 0 1 = [,2]
                        make Result<[Word, bool], ResolveError>.Success break
                    }
                }
                $i 1 + #i
            }
        }
    }
} }

fn resolve-call-word(ctx: .WordCtx, word: .P:Call) -> Result<Call, ResolveError> { block {
    $word match {
        case Local -> {
            @word
            $ctx.ctx $ctx.imports &word.generic-arguments resolve-types match {
                case Error -> { make Result<Call, ResolveError>.Error break }
                case Success -> {}
            } @resolved-generic-arguments
            &word.name Token-lexeme @name-len @name-ptr
            $ctx.ctx &word.name find-function match {
                case None -> { $ctx.ctx &word.name "function not found" abort<Call> break }
                case Some -> {}
            } @function
            $ctx.signatures $function.index Array:get<Signature> @signature
            $word.name $function $resolved-generic-arguments make Call
            make Result<Call, ResolveError>.Success break
        }
        case Foreign -> {
            @word
            $ctx.ctx $ctx.imports &word.generic-arguments resolve-types match {
                case Error -> { make Result<Call, ResolveError>.Error break }
                case Success -> {}
            } @resolved-generic-arguments
            $ctx.imports &word.module Token-lexeme Map:get-bs<Array<Import>> match {
                case None -> { $ctx.ctx &word.name "module not found" abort<Call> break }
                case Some -> {}
            } @imports
            0 @i
            loop {
                $imports.len $i = if {
                    $ctx.ctx &word.name "function not found" abort<Call> break
                }
                $imports $i Array:get<Import> @imp
                $ctx.ctx.resolved-modules $imp.module.index Array:get<Module> @module
                &module.functions &word.name Token-lexeme Map:get-bs-with-index<FunctionOrExtern> match {
                    case None -> {}
                    case Some -> {
                        [,] @function-id @function
                        $function FunctionOrExtern-signature @signature
                        $signature.generic-parameters.len $resolved-generic-arguments.len /= if {
                            $ctx.ctx &word.name "mismatched number of generic arguments" abort<Call> break
                        }
                        $word.name
                        $imp.module $function-id make FunctionHandle
                        $resolved-generic-arguments
                        make Call
                        make Result<Call, ResolveError>.Success break
                    }
                }
                $i 1 + #i
            }
        }
    }
} }

fn expect-arguments(ctx: .WordCtx, stack: .Stack, function-name: .Token, parameters: .Array<NamedType>) -> Maybe<ResolveError> { block {
    $parameters.len @i
    $ctx.arena Arena:temp @temp @save
    $temp $parameters.len Array:new-with-capacity<Type> @popped
    loop {
        $i 0 = if { make Maybe<ResolveError>.None break }
        $parameters $i 1 - Array:get<NamedType> .type @expected-type
        $ctx $stack pop match {
            case None -> { 1 1 = }
            case Some -> {
                @popped-type &popped-type $expected-type Type-eq not
            }
        } if {
            $ctx.ctx $function-name "mismatched arguments" abort-m break
        }
        $i 1 - #i
    }
    $temp $save Arena:restore
} }

fn find-function(ctx: .Ctx, name: .Token) -> Maybe<FunctionHandle> {
    0 @i
    0 @function-index
    loop {
        $i $ctx.top-items.len = if { make Maybe<FunctionHandle>.None break }
        $ctx.top-items $i Array:get<TopItem> match {
            case Function -> {
                @fun
                &fun.name Token-lexeme $name Token-lexeme Bytes:eq if {
                    $ctx.module-id $function-index make FunctionHandle
                    make Maybe<FunctionHandle>.Some break
                }
                $function-index 1 + #function-index
            }
            case Extern -> {
                @ext
                &ext.name Token-lexeme $name Token-lexeme Bytes:eq if {
                    $ctx.module-id $function-index make FunctionHandle
                    make Maybe<FunctionHandle>.Some break
                }
                $function-index 1 + #function-index
            }
            case _ -> { drop }
        }
        $i 1 + #i
    }
}

fn pop1(ctx: .WordCtx, stack: .Stack, token: .Token, expected-ptr: .i32, expected-len: i32) -> Result<Type, ResolveError> { block {
    $ctx $stack pop match {
        case None -> {
            $ctx.arena Fmt:into-new-string @msg @fmt
            &fmt $expected-ptr $expected-len Fmt:write
            &fmt ", got: []" Fmt:write
            $ctx.ctx $token $msg ~ ByteArray:unpack abort<Type> break
        }
        case Some -> {}
    } make Result<Type, ResolveError>.Success
} }

fn pop2(ctx: .WordCtx, stack: .Stack, token: .Token, expected-ptr: .i32, expected-len: i32) -> Result<[Type, Type], ResolveError> { block {
    $ctx $stack pop match {
        case None -> {
            $ctx.arena Fmt:into-new-string @msg @fmt
            &fmt $expected-ptr $expected-len Fmt:write
            &fmt ", got: []" Fmt:write
            $ctx.ctx $token $msg ~ ByteArray:unpack abort<[Type, Type]> break
        }
        case Some -> {}
    } @b
    $ctx $stack pop match {
        case None -> {
            $ctx.arena Fmt:into-new-string @msg @fmt
            &fmt $expected-ptr $expected-len Fmt:write
            &fmt ", got: [" Fmt:write
            &fmt $ctx.type-lookup &b Type-pretty
            &fmt "]" Fmt:write
            $ctx.ctx $token $msg ~ ByteArray:unpack abort<[Type, Type]> break
        }
        case Some -> {}
    } $b [,2] make Result<[Type, Type], ResolveError>.Success
} }

fn pop3(ctx: .WordCtx, stack: .Stack, token: .Token, expected-ptr: .i32, expected-len: i32) -> Result<[Type, Type, Type], ResolveError> { block {
    $ctx $stack pop match {
        case None -> {
            $ctx.arena Fmt:into-new-string @msg @fmt
            &fmt $expected-ptr $expected-len Fmt:write
            &fmt ", got: []" Fmt:write
            $ctx.ctx $token $msg ~ ByteArray:unpack abort<[Type, Type, Type]> break
        }
        case Some -> {}
    } @c
    $ctx $stack pop match {
        case None -> {
            $ctx.arena Fmt:into-new-string @msg @fmt
            &fmt $expected-ptr $expected-len Fmt:write
            &fmt ", got: [" Fmt:write
            &fmt $ctx.type-lookup &c Type-pretty
            &fmt "]" Fmt:write
            $ctx.ctx $token $msg ~ ByteArray:unpack abort<[Type, Type, Type]> break
        }
        case Some -> {}
    } @b
    $ctx $stack pop match {
        case None -> {
            $ctx.arena Fmt:into-new-string @msg @fmt
            &fmt $expected-ptr $expected-len Fmt:write
            &fmt ", got: [" Fmt:write
            &fmt $ctx.type-lookup &b Type-pretty
            &fmt ", " Fmt:write
            &fmt $ctx.type-lookup &c Type-pretty
            &fmt "]" Fmt:write
            $ctx.ctx $token $msg ~ ByteArray:unpack abort<[Type, Type, Type]> break
        }
        case Some -> {}
    } $b $c [,3] make Result<[Type, Type, Type], ResolveError>.Success
} }

fn expect1(ctx: .WordCtx, stack: .Stack, token: .Token, a: .Type, expected-ptr: .i32, expected-len: i32) -> Maybe<ResolveError> { block {
    $ctx $stack $token $expected-ptr $expected-len pop1 match {
        case Error -> { make Maybe<ResolveError>.Some break }
        case Success -> {}
    } @actual-a
    &actual-a $a Type-eq not if {
        $ctx.arena Fmt:into-new-string @msg @fmt
        &fmt $expected-ptr $expected-len Fmt:write
        &fmt ", got: [" Fmt:write &fmt $ctx.type-lookup &actual-a Type-pretty &fmt "]" Fmt:write
        $ctx.ctx $token $msg ~ ByteArray:unpack abort-m
    } else {
        make Maybe<ResolveError>.None
    }
} }
fn expect2(ctx: .WordCtx, stack: .Stack, token: .Token, a: .Type, b: .Type, expected-ptr: .i32, expected-len: i32) -> Maybe<ResolveError> { block {
    $ctx $stack $token $expected-ptr $expected-len pop2 match {
        case Error -> { make Maybe<ResolveError>.Some break }
        case Success -> {}
    } [,] @actual-b @actual-a
    block {
        &actual-b $b Type-eq not if { 1 1 = break }
        &actual-a $a Type-eq not if { 1 1 = break }
        0 1 =
    } if {
        $ctx.arena Fmt:into-new-string @msg @fmt
        &fmt $expected-ptr $expected-len Fmt:write
        &fmt ", got: [" Fmt:write
        &fmt $ctx.type-lookup &actual-a Type-pretty &fmt ", " Fmt:write
        &fmt $ctx.type-lookup &actual-b Type-pretty &fmt "]"  Fmt:write
        $ctx.ctx $token $msg ~ ByteArray:unpack abort-m
    } else {
        make Maybe<ResolveError>.None
    }
} }
fn expect3(ctx: .WordCtx, stack: .Stack, token: .Token, a: .Type, b: .Type, c: .Type, expected-ptr: .i32, expected-len: i32) -> Maybe<ResolveError> { block {
    $ctx $stack $token $expected-ptr $expected-len pop3 match {
        case Error -> { make Maybe<ResolveError>.Some break }
        case Success -> {}
    } [,] @actual-c @actual-b @actual-a
    block {
        &actual-c $c Type-eq not if { 1 1 = break }
        &actual-b $b Type-eq not if { 1 1 = break }
        &actual-a $a Type-eq not if { 1 1 = break }
        0 1 =
    } if {
        $ctx.arena Fmt:into-new-string @msg @fmt
        &fmt $expected-ptr $expected-len Fmt:write
        &fmt ", got: [" Fmt:write
        &fmt $ctx.type-lookup &actual-a Type-pretty &fmt ", " Fmt:write
        &fmt $ctx.type-lookup &actual-b Type-pretty &fmt ", " Fmt:write
        &fmt $ctx.type-lookup &actual-c Type-pretty &fmt "]"  Fmt:write
        $ctx.ctx $token $msg ~ ByteArray:unpack abort-m
    } else {
        make Maybe<ResolveError>.None
    }
} }

fn resolve-init-local(ctx: .WordCtx, stack: .Stack, word: .P:InitLocal) -> Result<[Word, bool], ResolveError> { block {
    $ctx $stack pop match {
        case None -> { $ctx.ctx &word.start "local init expected item on stack" abort<[Word, bool]> break }
        case Some -> {}
    } @type
    $ctx.struc-lit-ctx match {
        case None -> {}
        case Some -> {
            @struc-lit-ctx
            &struc-lit-ctx.fields &word.ident Token-lexeme Map:remove-bs<Type> match {
                case None -> {}
                case Some -> {
                    @field-type
                    $ctx &struc-lit-ctx.generic-arguments &field-type resolve-generic @field-type
                    &type &field-type Type-eq not if {
                        $ctx.ctx &word.ident "expected different type for field" abort<[Word, bool]> break
                    }
                    make FieldInit {
                        $field-type @type
                        $word.ident @name
                        $struc-lit-ctx.generic-arguments @generic-arguments
                        $struc-lit-ctx.struc @struc
                    }
                    make Word.FieldInit 0 1 = [,2]
                    make Result<[Word, bool], ResolveError>.Success break
                }
            }
        }
    }
    make Env:Local {
        $type @type
        $word.ident @name
        0 1 = @reffed
        0 1 = @is-parameter
    } @local
    $ctx.arena &ctx.env $local Env:insert @local-id
    $word.ident
    $type
    $local-id
    make InitLocal
    make Word.InitLocal 0 1 = [,2]
    make Result<[Word, bool], ResolveError>.Success
} }

fn resolve-get-local(ctx: .WordCtx, stack: .Stack, word: .P:GetLocal) -> Result<[Word, bool], ResolveError> { block {
    &ctx.env &word.ident Env:lookup match {
        case None -> {
            $ctx.globals &word.ident Token-lexeme Map:get-bs-with-index<Global> match {
                case None -> { $ctx.ctx &word.ident "var not found" abort<[Word, bool]> break }
                case Some -> {
                    [,] @id @globl
                    $globl.type $word.ident $ctx.ctx.module-id $id make GlobalId make VarId.Global
                }
            }
        }
        case Some -> {
            @local
            $local.local.type $local.id make VarId.Local
        }
    } @var-id @type
    $ctx $type &word.fields resolve-field-accesses match {
        case Error -> { make Result<[Word, bool], ResolveError>.Error break }
        case Success -> {}
    } @fields
    $fields.len 0 = if {
        $type
    } else {
        &fields $fields.len 1 - Array:get<FieldAccess> .target-taip ~
    } @resolved-type
    $ctx $stack $resolved-type push
    $word.ident $var-id $type $fields $resolved-type make GetLocal
    make Word.GetLocal 0 1 = [,2]
    make Result<[Word, bool], ResolveError>.Success
} }

fn resolve-ref-local(ctx: .WordCtx, stack: .Stack, word: .P:RefLocal) -> Result<[Word, bool], ResolveError> { block {
    &ctx.env &word.ident Env:lookup match {
        case None -> {
            $ctx.globals &word.ident Token-lexeme Map:get-bs-with-index<Global> match {
                case None -> { $ctx.ctx &word.ident "var not found" abort<[Word, bool]> break }
                case Some -> {
                    [,] @id @globl
                    $globl.type $word.ident $ctx.ctx.module-id $id make GlobalId make VarId.Global
                }
            }
        }
        case Some -> {
            @local
            1 1 = #local.local.reffed
            $local.local.type $local.id make VarId.Local
        }
    } @var-id @type
    $ctx $type &word.fields resolve-field-accesses match {
        case Error -> { make Result<[Word, bool], ResolveError>.Error break }
        case Success -> {}
    } @fields
    $fields.len 0 = if {
        &type
    } else {
        &fields $fields.len 1 - Array:get<FieldAccess> .target-taip
    } $ctx.arena flip Arena:copy<Type> @result-type
    $ctx $stack $result-type make Type.Ptr push
    $word.ident $var-id $fields make RefLocal
    make Word.RefLocal 0 1 = [,2]
    make Result<[Word, bool], ResolveError>.Success
} }

fn resolve-field-accesses(ctx: .WordCtx, type: Type, fields: .Array<Token>) -> Result<Array<FieldAccess>, ResolveError> { block {
    Array:new<FieldAccess> @resolved
    $fields.len 0 = if { $resolved make Result<Array<FieldAccess>, ResolveError>.Success break }
    0 @i
    loop {
        $fields $i Array:get<Token> @field-name
        $type match {
            case Custom -> {
                @custom-type
                $ctx.type-lookup $custom-type.type-definition lookup-type match {
                    case Variant -> {
                        drop $ctx.ctx $field-name "can not access fields of a variant" abort<Array<FieldAccess>> break
                    }
                    case Struct -> {}
                } $custom-type
            }
            case Ptr -> {
                ~ match {
                    case Custom -> {
                        @custom-type
                        $ctx.type-lookup $custom-type.type-definition lookup-type match {
                            case Variant -> { drop $ctx.ctx $field-name "TODO" abort<Array<FieldAccess>> break }
                            case Struct -> {}
                        }
                        $custom-type
                    }
                    case _ -> { drop $ctx.ctx $field-name "TODO" abort<Array<FieldAccess>> break }
                }
            }
            case _ -> { drop $ctx.ctx $field-name "TODO" abort<Array<FieldAccess>> break }
        }
        @custom-type @struc
        0 @field-index
        loop {
            $field-index $struc.fields.len = if {
                $ctx.ctx $field-name "field not found" abort-m break
            }
            &struc.fields $field-index Array:get<NamedType> @field
            &field.name Token-lexeme $field-name Token-lexeme Bytes:eq if {
                $ctx.arena &resolved
                $ctx &custom-type.generic-arguments &field.type resolve-generic @target-type
                $field-name ~ $type $target-type $field-index make FieldAccess
                Array:push<FieldAccess>
                $target-type #type
                make Maybe<ResolveError>.None break
            }
            $field-index 1 + #field-index
        } match {
            case Some -> { make Result<Array<FieldAccess>, ResolveError>.Error break }
            case None -> {}
        }
        $i 1 + #i
        $i $fields.len = if { $resolved make Result<Array<FieldAccess>, ResolveError>.Success break }
    }
} }

fn resolve-generic(ctx: .WordCtx, generics: .Array<Type>, type: .Type) -> Type {
    $type match {
        case Generic -> { .index ~ $generics flip Array:get<Type> ~ }
        case Ptr -> {
            ~ $ctx flip $generics flip resolve-generic $ctx.arena flip Arena:push<Type> make Type.Ptr
        }
        case Custom -> {
            @type
            $type.type-definition
            $ctx $generics &type.generic-arguments resolve-generic-many
            make CustomTypeType
            make Type.Custom
        }
        case Fun -> {
            @type
            $type.token
            $ctx $generics &type.parameters resolve-generic-many
            $ctx $generics &type.returns resolve-generic-many
            make FunType
            make Type.Fun
        }
        case Tuple -> {
            @tuple
            $tuple.token
            $ctx $generics &tuple.items resolve-generic-many
            make TupleType
            make Type.Tuple
        }
        case Bool -> { make Type.Bool }
        case I32 -> { make Type.I32 }
        case I64 -> { make Type.I64 }
    }
}
fn resolve-generic-many(ctx: .WordCtx, generics: .Array<Type>, types: .Array<Type>) -> Array<Type> {
    Array:new<Type> @resolved
    0 @i
    loop {
        $i $types.len = if { break }
        $ctx.arena &resolved $ctx $generics $types $i Array:get<Type> resolve-generic Array:push<Type>
        $i 1 + #i
    }
    $resolved
}

fn resolve-imports(ctx: .Ctx) -> Result<Map<ByteSlice, Array<Import>>, ResolveError> {
    $ctx.arena Arena:temp @temp-arena @save
    \ByteSlice:eq-by-ref Map:new<ByteSlice, Array<Import>> @resolved-imports
    &ctx.parsed-modules.keys $ctx.module-id.index Array:get<ByteSlice> @module-path
    0 @i
    loop {
        $ctx.top-items.len $i = if { $resolved-imports make Result<Map<ByteSlice, Array<Import>>, ResolveError>.Success break }
        $ctx.top-items $i Array:get<TopItem> match {
            case Import -> {
                @imp
                $module-path ~ ByteSlice:unpack "-" Bytes:eq if {
                    "" ByteArray:init
                } else {
                    $module-path ~ ByteSlice:unpack FS:path-dir ByteArray:init
                } @path
                $temp-arena &path &imp.path Token-lexeme 2 - flip 1 + flip ByteArray:push
                $temp-arena $path ByteSlice:unpack FS:normalize make ByteSlice @path
                $ctx.parsed-modules &path Map:get-index<ByteSlice, Array<TopItem>> assert-some<i32> make ModuleId @imported-module-id
                $ctx.arena $path ByteSlice:copy @path

                $ctx $imported-module-id &imp.items resolve-import-items match {
                    case Error -> { make Result<Map<ByteSlice, Array<Import>>, ResolveError>.Error break }
                    case Success -> {}
                } @items

                &imp.qualifier Token-lexeme make ByteSlice @qualifier
                $imp.start $imported-module-id $path $imp.qualifier $items make Import @imp
                &resolved-imports &qualifier Map:get<ByteSlice, Array<Import>> match {
                    case None -> {
                        $ctx.arena &resolved-imports $qualifier $ctx.arena $imp Array:singleton<Import> Map:insert<ByteSlice, Array<Import>> drop
                    }
                    case Some -> { $ctx.arena flip $imp Array:push<Import> }
                }
            }
            case _ -> { drop }
        }
        $i 1 + #i
    }
    $temp-arena $save Arena:restore
}

fn resolve-import-items(ctx: .Ctx, imported-module-id: ModuleId, items: .Array<Token>) -> Result<Array<ImportItem>, ResolveError> {
    Array:new<ImportItem> @resolved-items
    $ctx.parsed-modules $imported-module-id.index Map:get-value<ByteSlice, Array<TopItem>> @imported-module
    0 @i
    loop {
        $items.len $i = if { $resolved-items make Result<Array<ImportItem>, ResolveError>.Success break }
        $items $i Array:get<Token> @item
        $imported-module $imported-module-id $item lookup-item-in-module match {
            case None -> {
                $ctx $item "not found" abort<Array<ImportItem>> break
            }
            case Some -> {}
        } @item
        $ctx.arena &resolved-items $item Array:push<ImportItem>
        $i 1 + #i
    }
}

fn lookup-item-in-module(module: .Array<TopItem>, module-id: ModuleId, name: .Token) -> Maybe<ImportItem> {
    0 @type-index
    0 @function-index
    0 @other
    loop {
        $type-index $function-index + $other + $module.len = if { make Maybe<ImportItem>.None break }
        $module $type-index $function-index + $other + Array:get<TopItem> match {
            case Struct -> {
                .name Token-lexeme $name Token-lexeme Bytes:eq if {
                    $name ~ $module-id $type-index make CustomTypeHandle make ImportItemItem.CustomType make ImportItem make Maybe<ImportItem>.Some break
                }
                $type-index 1 + #type-index
            }
            case Variant -> {
                .name Token-lexeme $name Token-lexeme Bytes:eq if {
                    $name ~ $module-id $type-index make CustomTypeHandle make ImportItemItem.CustomType make ImportItem make Maybe<ImportItem>.Some break
                }
                $type-index 1 + #type-index
            }
            case Function -> {
                .name Token-lexeme $name Token-lexeme Bytes:eq if {
                    $name ~ $module-id $function-index make FunctionHandle make ImportItemItem.Function make ImportItem make Maybe<ImportItem>.Some break
                }
                $function-index 1 + #function-index
            }
            case _ -> {
                drop $other 1 + #other
            }
        }
    }
}

fn resolve-custom-types(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>) -> Result<Map<ByteSlice, CustomType>, ResolveError> {
    \ByteSlice:eq-by-ref Map:new<ByteSlice, CustomType> @resolved-custom-types
    0 @i
    loop {
        $ctx.top-items.len $i = if { $resolved-custom-types make Result<Map<ByteSlice, CustomType>, ResolveError>.Success break }
        $ctx.top-items $i Array:get<TopItem> match {
            case Struct -> {
                @struc
                $ctx $imports $struc resolve-struct match {
                    case Error -> { make Result<Map<ByteSlice, CustomType>, ResolveError>.Error break }
                    case Success -> {
                        $ctx.arena flip
                        &resolved-custom-types flip
                        &struc.name Token-lexeme make ByteSlice flip
                        make CustomType.Struct Map:insert<ByteSlice, CustomType> drop }
                }
            }
            case Variant -> {
                @varant
                $ctx $imports $varant resolve-variant match {
                    case Error -> { make Result<Map<ByteSlice, CustomType>, ResolveError>.Error break }
                    case Success -> {
                        $ctx.arena flip
                        &resolved-custom-types flip
                        &varant.name Token-lexeme make ByteSlice flip
                        make CustomType.Variant Map:insert<ByteSlice, CustomType> drop
                    }
                }
            }
            case _ -> { drop }
        }
        $i 1 + #i
    }
}

fn resolve-signatures(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>) -> Result<Array<Signature>, ResolveError> {
    Array:new<Signature> @signatures
    0 @i
    loop () -> Result<Array<Signature>, ResolveError> {
        $ctx.top-items.len $i = if { $signatures make Result<Array<Signature>, ResolveError>.Success break }
        $ctx.top-items $i Array:get<TopItem> match {
            case Extern -> { .signature make Maybe<.P:Signature>.Some }
            case Function -> { .signature make Maybe<.P:Signature>.Some }
            case _ -> { drop make Maybe<.P:Signature>.None }
        } match {
            case None -> {}
            case Some -> {
                @signature
                $ctx.arena &signatures
                $ctx $imports $signature resolve-signature match {
                    case Error -> { make Result<Array<Signature>, ResolveError>.Error break }
                    case Success -> {}
                }
                Array:push<Signature>
            }
        }
        $i 1 + #i
    }
}

fn resolve-signature(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, signature: .P:Signature) -> Result<Signature, ResolveError> { block () -> Result<Signature, ResolveError> {
    $signature.generic-parameters
    $ctx $imports &signature.parameters resolve-named-types match {
        case Error -> { make Result<Signature, ResolveError>.Error break }
        case Success -> {}
    }
    $ctx $imports &signature.returns resolve-types match {
        case Error -> { make Result<Signature, ResolveError>.Error break }
        case Success -> {}
    }
    make Signature
    make Result<Signature, ResolveError>.Success
} }

fn resolve-struct(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, struc: .P:Struct) -> Result<Struct, ResolveError> { block () -> Result<Struct, ResolveError> {
    $struc.name $struc.generic-parameters
    Array:new<NamedType> @fields
    0 @i
    loop () -> Result<Array<NamedType>, ResolveError> {
        $i $struc.fields.len = if { $fields make Result<Array<NamedType>, ResolveError>.Success break }
        &struc.fields $i Array:get<P:NamedType> @field
        $ctx.arena &fields
        $ctx $imports $field resolve-named-type match {
            case Error -> { make Result<Array<NamedType>, ResolveError>.Error break }
            case Success -> {}
        }
        Array:push<NamedType>
        $i 1 + #i
    } match {
        case Error -> { make Result<Struct, ResolveError>.Error break }
        case Success -> {}
    }
    make Struct make Result<Struct, ResolveError>.Success
} }

fn resolve-variant(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, varant: .P:Variant) -> Result<Variant, ResolveError> { block () -> Result<Variant, ResolveError> {
    $varant.name $varant.generic-parameters
    Array:new<VariantCase> @cases
    0 @i
    loop () -> Result<Array<VariantCase>, ResolveError> {
        $i $varant.cases.len = if { $cases make Result<Array<VariantCase>, ResolveError>.Success break }
        &varant.cases $i Array:get<P:VariantCase> @cays
        $ctx.arena &cases
        $cays.name
        &cays.type match {
            case None -> { make Maybe<Type>.None }
            case Some -> { $ctx flip $imports flip resolve-type match {
                case Error -> { make Result<Array<VariantCase>, ResolveError>.Error break }
                case Success -> { make Maybe<Type>.Some }
            } }
        }
        make VariantCase
        Array:push<VariantCase>
        $i 1 + #i
    } match {
        case Error -> { make Result<Variant, ResolveError>.Error break }
        case Success -> {}
    }
    make Variant make Result<Variant, ResolveError>.Success
} }

fn resolve-named-type(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, type: .P:NamedType) -> Result<NamedType, ResolveError> {
    $type.name
    $ctx $imports &type.type resolve-type match {
        case Error -> { flip drop make Result<NamedType, ResolveError>.Error }
        case Success -> { make NamedType make Result<NamedType, ResolveError>.Success }
    }
}
fn resolve-named-types(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, named-types: .Array<P:NamedType>) -> Result<Array<NamedType>, ResolveError> { block {
    Array:new<NamedType> @resolved-named-types
    0 @i
    loop {
        $i $named-types.len = if { $resolved-named-types make Result<Array<NamedType>, ResolveError>.Success break }
        $ctx $imports $named-types $i Array:get<P:NamedType> resolve-named-type match {
            case Error -> { make Result<Array<NamedType>, ResolveError>.Error break }
            case Success -> { $ctx.arena flip &resolved-named-types flip Array:push<NamedType> }
        }
        $i 1 + #i
    }
} }

fn resolve-type(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, type: .P:Type) -> Result<Type, ResolveError> { block () -> Result<Type, ResolveError> {
    $type match {
        case Bool -> { drop make Type.Bool }
        case I32 -> { drop make Type.I32 }
        case I64 -> { drop make Type.I64 }
        case Generic -> { ~ make Type.Generic }
        case Ptr -> {
            .type ~ $ctx flip $imports flip resolve-type match {
                case Error -> { make Result<Type, ResolveError>.Error break }
                case Success -> { $ctx.arena flip Arena:push<Type> make Type.Ptr }
            }
        }
        case Custom -> { $ctx flip $imports flip resolve-custom-type match {
                case Error -> { make Result<Type, ResolveError>.Error break }
                case Success -> { make Type.Custom }
        } }
        case Fun -> {
            @fun
            $fun.token
            $ctx $imports &fun.parameters resolve-types match {
                case Error -> { make Result<Type, ResolveError>.Error break }
                case Success -> {}
            }
            $ctx $imports &fun.returns resolve-types match {
                case Error -> { make Result<Type, ResolveError>.Error break }
                case Success -> {}
            }
            make FunType
            make Type.Fun
        }
        case Tuple -> {
            @tuple
            $tuple.token
            $ctx $imports &tuple.items resolve-types match {
                case Error -> { make Result<Type, ResolveError>.Error break }
                case Success -> {}
            }
            make TupleType
            make Type.Tuple
        }
    } make Result<Type, ResolveError>.Success
} }

fn resolve-custom-type(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, custom-type: .P:CustomType) -> Result<CustomTypeType, ResolveError> { block {
    $custom-type match {
        case Local -> {
            @type
            $ctx $imports &type.generic-arguments resolve-types match {
                case Error -> { make Result<CustomTypeType, ResolveError>.Error break }
                case Success -> {}
            } @generic-arguments
            0 @i
            0 @type-index
            loop {
                $ctx.top-items.len $i = if {
                    0 @i
                    loop {
                        $imports.values.len $i = if { $ctx &type.name "not found" abort<CustomTypeType> break }
                        $imports $i Map:get-value<ByteSlice, Array<Import>> @imports
                        0 @j
                        loop {
                            $j $imports.len = if { make Maybe<CustomTypeType>.None break }
                            $imports $j Array:get<Import> @imp
                            0 @k
                            loop {
                                $k $imp.items.len = if { make Maybe<CustomTypeType>.None break }
                                &imp.items $k Array:get<ImportItem> @item
                                &item.name Token-lexeme &type.name Token-lexeme Bytes:eq if {
                                    &item.handle match {
                                        case CustomType -> {
                                            ~
                                            $generic-arguments
                                            make CustomTypeType make Maybe<CustomTypeType>.Some break
                                        }
                                        case _ -> { drop }
                                    }
                                }
                                $k 1 + #k
                            } match {
                                case None -> {}
                                case _ -> { break }
                            }
                            $j 1 + #j
                        } match {
                            case None -> {}
                            case Some -> { make Result<CustomTypeType, ResolveError>.Success break }
                        }
                        $i 1 + #i
                    } break
                }
                $ctx.top-items $i Array:get<TopItem>
                match {
                    case Struct -> {
                        .name Token-lexeme &type.name Token-lexeme Bytes:eq if {
                            $ctx.module-id $type-index make CustomTypeHandle
                            $generic-arguments
                            make CustomTypeType
                            make Result<CustomTypeType, ResolveError>.Success break
                        }
                        $type-index 1 + #type-index
                    }
                    case Variant -> {
                        .name Token-lexeme &type.name Token-lexeme Bytes:eq if {
                            $ctx.module-id $type-index make CustomTypeHandle
                            $generic-arguments
                            make CustomTypeType
                            make Result<CustomTypeType, ResolveError>.Success break
                        }
                        $type-index 1 + #type-index
                    }
                    case _ -> { drop }
                }
                $i 1 + #i
            }
        }
        case Foreign -> {
            @type
            $ctx $imports &type.generic-arguments resolve-types match {
                case Error -> { make Result<CustomTypeType, ResolveError>.Error break }
                case Success -> {}
            } @generic-arguments
            &type.module Token-lexeme make ByteSlice @module-lexeme
            $imports &module-lexeme Map:get<ByteSlice, Array<Import>> match {
                case None -> {
                    $ctx &type.module "not found" abort<CustomTypeType> break
                }
                case Some -> {
                    @imports
                    0 @i
                    loop {
                        $i $imports.len = if { $ctx &type.name "not found" abort<CustomTypeType> break }
                        $imports $i Array:get<Import> @imp
                        $ctx.resolved-modules $imp.module.index Array:get<Module> @module
                        0 @j
                        loop {
                            $j $module.custom-types.keys.len = if { make Maybe<CustomTypeType>.None break }
                            &module.custom-types $j Map:get-value<ByteSlice, CustomType> match {
                                case Struct -> {
                                    .name Token-lexeme &type.name Token-lexeme Bytes:eq if {
                                        $imp.module $i make CustomTypeHandle
                                        $generic-arguments
                                        make CustomTypeType
                                        make Maybe<CustomTypeType>.Some break
                                    }
                                }
                                case Variant -> {
                                    .name Token-lexeme &type.name Token-lexeme Bytes:eq if {
                                        $imp.module $i make CustomTypeHandle
                                        $generic-arguments
                                        make CustomTypeType
                                        make Maybe<CustomTypeType>.Some break
                                    }
                                }
                                case _ -> { drop }
                            }
                            $j 1 + #j
                        } match {
                            case Some -> { make Result<CustomTypeType, ResolveError>.Success break }
                            case None -> { $ctx &type.name "not found" abort<CustomTypeType> break }
                        }
                        $i 1 + #i
                    }
                }
            }
        }
    }
} }

fn resolve-types(ctx: .Ctx, imports: .Map<ByteSlice, Array<Import>>, types: .Array<P:Type>) -> Result<Array<Type>, ResolveError> { block {
    Array:new<Type> @resolved-types
    0 @i
    loop {
        $i $types.len = if { $resolved-types make Result<Array<Type>, ResolveError>.Success break }
        $ctx $imports $types $i Array:get<P:Type> resolve-type match {
            case Error -> { make Result<Array<Type>, ResolveError>.Error break }
            case Success -> { $ctx.arena flip &resolved-types flip Array:push<Type> }
        }
        $i 1 + #i
    }
} }

